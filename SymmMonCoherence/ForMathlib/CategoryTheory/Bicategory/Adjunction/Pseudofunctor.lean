/-
Copyright (c) 2026 Robin Carlier. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Robin Carlier
-/
module

public import Mathlib.CategoryTheory.Bicategory.Adjunction.Basic
public import Mathlib.CategoryTheory.Bicategory.Functor.Pseudofunctor
-- public import SymmMonCoherence.ForMathlib.Tactic.CategoryTheory.RotateIso

/-!
# Pseudofunctors map equivalences to equivalences and adjunctions to adjunctions

In this file, we provide `Pseudofunctor.mapAdj` and `Pseudofunctor.mapEquiv` as
bicategorical analogues of `Functor.mapIso`. -/

@[expose] public section

universe wâ‚ wâ‚‚ vâ‚ vâ‚‚ uâ‚ uâ‚‚

namespace CategoryTheory

namespace Pseudofunctor

open Bicategory

variable {C : Type uâ‚} {D : Type uâ‚‚} [Bicategory.{wâ‚, vâ‚} C] [Bicategory.{wâ‚‚, vâ‚‚} D] (F : C â¥¤áµ– D)

section

@[reassoc]
lemma mapâ‚‚_associator_inv {a b c d : C} (f : a âŸ¶ b) (g : b âŸ¶ c) (h : c âŸ¶ d) :
    F.mapâ‚‚ (Î±_ f g h).inv =
    (F.mapComp f (g â‰« h)).hom â‰« F.map f â— (F.mapComp g h).hom â‰«
      (Î±_ (F.map f) (F.map g) (F.map h)).inv â‰« (F.mapComp f g).inv â–· F.map h â‰«
      (F.mapComp (f â‰« g) h).inv := by
  simp [â† IsIso.inv_eq_inv, â† PrelaxFunctor.mapâ‚‚_inv]

@[reassoc]
lemma mapâ‚‚_left_unitor_inv {a b : C} (f : a âŸ¶ b) :
    F.mapâ‚‚ (Î»_ f).inv =
    (Î»_ (F.map f)).inv â‰« (F.mapId a).inv â–· F.map f â‰« (F.mapComp (ğŸ™ a) f).inv := by
  simp [â† IsIso.inv_eq_inv, â† PrelaxFunctor.mapâ‚‚_inv]

@[reassoc]
lemma mapâ‚‚_right_unitor_inv {a b : C} (f : a âŸ¶ b) :
    F.mapâ‚‚ (Ï_ f).inv =
    (Ï_ (F.map f)).inv â‰«  F.map f â— (F.mapId b).inv â‰« (F.mapComp f (ğŸ™ b)).inv := by
  simp [â† IsIso.inv_eq_inv, â† PrelaxFunctor.mapâ‚‚_inv]

end
/-- A pseudofunctor maps an adjunction in the source bicategory to an adjunction
in the target bicategory. -/
@[simps]
def mapAdj {x y : C} {f : x âŸ¶ y} {g : y âŸ¶ x} (adj : f âŠ£ g) :
    F.map f âŠ£ F.map g where
  unit := (F.mapId _).inv â‰« F.mapâ‚‚ (adj.unit) â‰« (F.mapComp _ _).hom
  counit := (F.mapComp _ _).inv â‰« F.mapâ‚‚ (adj.counit) â‰« (F.mapId _).hom
  left_triangle := by
    have := congr(F.mapâ‚‚ $(adj.left_triangle)) =â‰« (F.mapComp f (ğŸ™ y)).hom
    dsimp [leftZigzag, bicategoricalComp] at this âŠ¢
    simp only [whiskerRight_comp, id_whiskerRight, Category.id_comp, Iso.inv_hom_id,
      Category.comp_id, PrelaxFunctor.mapâ‚‚_comp, mapâ‚‚_whisker_right, mapâ‚‚_associator,
      mapâ‚‚_whisker_left, Category.assoc, Iso.inv_hom_id_assoc, mapâ‚‚_left_unitor,
      Iso.cancel_iso_hom_left, comp_whiskerRight, whiskerLeft_comp] at this âŠ¢
    simp [reassoc_of% this, inv_hom_whiskerRight_assoc, mapâ‚‚_right_unitor_inv]
  right_triangle := by
    have eâ‚ := congr(F.mapâ‚‚ $(adj.right_triangle)) =â‰« (F.mapComp (ğŸ™ y) g).hom
    dsimp [rightZigzag, bicategoricalComp] at eâ‚ âŠ¢
    simp only [whiskerRight_comp, id_whiskerRight, Category.id_comp, Iso.inv_hom_id,
      PrelaxFunctor.mapâ‚‚_comp, mapâ‚‚_whisker_left, mapâ‚‚_whisker_right, Category.assoc,
      Category.comp_id, mapâ‚‚_right_unitor, Iso.cancel_iso_hom_left, whiskerLeft_comp,
      comp_whiskerRight] at eâ‚ âŠ¢
    simp_rw [mapâ‚‚_associator_inv, Category.assoc, Iso.inv_hom_id_assoc] at eâ‚
    simp [reassoc_of% eâ‚, mapâ‚‚_left_unitor_inv]

@[simps]
def _root_.CategoryTheory.Bicategory.Equivalence.adjunction {x y : C} (e : x â‰Œ y) :
    e.hom âŠ£ e.inv where
  unit := e.unit.hom
  counit := e.counit.hom
  left_triangle := congr($(e.left_triangle).hom)
  right_triangle := congr($(e.right_triangle).hom)

@[simps]
/- The inverse equivalence of an equivalence internal to a bicategory. -/
def _root_.CategoryTheory.Bicategory.Equivalence.symm {x y : C} (e : x â‰Œ y) :
    y â‰Œ x where
  hom := e.inv
  inv := e.hom
  unit := e.counit.symm
  counit := e.unit.symm
  left_triangle := by
    have := congr(Iso.symm <| $e.right_triangle)
    dsimp [leftZigzagIso, rightZigzagIso, bicategoricalIsoComp] at this âŠ¢
    simpa using this

@[simps]
/- Transitivity of equivalence. -/
def _root_.CategoryTheory.Bicategory.Equivalence.trans {x y z: C} (e : x â‰Œ y) (e' : y â‰Œ z) :
    x â‰Œ z where
  hom := e.hom â‰« e'.hom
  inv := e'.inv â‰« e.inv
  unit :=
    e.unit â‰ªâ‰«
      whiskerLeftIso e.hom (Î»_ _).symm
      â‰ªâ‰« whiskerLeftIso e.hom (whiskerRightIso e'.unit _)
      â‰ªâ‰« whiskerLeftIso e.hom (Î±_ _ _ _)
      â‰ªâ‰« (Î±_ _ _ _).symm
  counit :=
    (Î±_ _ _ _)
      â‰ªâ‰« whiskerLeftIso e'.inv (Î±_ _ _ _).symm
      â‰ªâ‰« whiskerLeftIso e'.inv (whiskerRightIso e.counit _)
      â‰ªâ‰« whiskerLeftIso e'.inv (Î»_ _)
      â‰ªâ‰« e'.counit
  left_triangle := by
    ext
    dsimp
    have := e.adjunction.comp e'.adjunction |>.left_triangle
    simpa [leftZigzag, bicategoricalComp] using this

lemma _root_.CategoryTheory.Bicategory.Equivalence.trans_adjunction
    {x y z: C} (e : x â‰Œ y) (e' : y â‰Œ z) :
    (e.trans e').adjunction = e.adjunction.comp e'.adjunction := by
  ext
  Â· simp [bicategoricalComp]
  Â· simp [bicategoricalComp]

lemma _root_.CategoryTheory.Bicategory.Equivalence.symm_trans_adjunction
    {x y z: C} (e : x â‰Œ y) (e' : y â‰Œ z) :
    (e.trans e').symm.adjunction = e'.symm.adjunction.comp e.symm.adjunction := by
  ext
  Â· simp [bicategoricalComp]
  Â· simp [bicategoricalComp]

/-- A pseudofunctor maps an equivalence in the source bicategory to an equivalence
in the target bicategory. This is the bicategorical version of `Functor.mapIso`. -/
@[simps]
def mapEquivalence {x y : C} (e : x â‰Œ y) :
    F.obj x â‰Œ F.obj y where
  hom := F.map e.hom
  inv := F.map e.inv
  unit := (F.mapId _).symm â‰ªâ‰« F.mapâ‚‚Iso e.unit â‰ªâ‰« (F.mapComp _ _)
  counit := (F.mapComp _ _).symm â‰ªâ‰« F.mapâ‚‚Iso e.counit â‰ªâ‰« (F.mapId _)
  left_triangle := by
    ext
    exact (F.mapAdj (e.adjunction)).left_triangle

end CategoryTheory.Pseudofunctor
