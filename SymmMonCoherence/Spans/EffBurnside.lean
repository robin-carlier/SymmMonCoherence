/-
Copyright (c) 2025 Robin Carlier. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Robin Carlier
-/
module

public import SymmMonCoherence.Spans.Inclusions
public import Mathlib.CategoryTheory.Bicategory.LocallyGroupoid
public import Mathlib.CategoryTheory.Bicategory.Opposites

/-! # Effective Burnside (2,1)-categories

In this file, we define the effective Burnside (2,1)-category of a category with pullbacks
as an abbreviation for the Pith of the bicategory of spans in C .

Spelled explicitly, this is a bicategory where
- objects are (a type alias for) objects of `C`
- 1-morphisms `c ‚Üí c'` are spans `c ‚Üê x ‚Üí c'` in `C`.
- 2-morphisms are isomorphisms of spans, i.e digrams
  ```
      c
    ‚Üó ‚Üë
  x ‚Üí y
    ‚Üò ‚Üì
      c'
  ```
  where the horizontal morphism is an isomorphism.

## Terminological note

The name "Burnside category" comes from the litterature on equivariant homotopy
theory and more specifically the theory of Mackey functors, where local group-completion of
the pith of the bicategory of spans on finite G-sets is called the "Burnside category of G",
as a categorificaton of the Burnside ring of G
(which itself is the group-completion of the semiring of isomorphisms classes of finite G-sets).
The terminology "effective Burnside ‚àû-category" was then coined by Barwick and used
without mention of any group (like we do) for the (‚àû,1)-categorical analogue of the pith of the
category of spans on a category. When interpreting locally groupoidal bicategories as
(‚àù,1)-categories, we recover Barwick's object.
-/

@[expose] public section

namespace CategoryTheory

/--
The effective Burnside bicategory of a category with pullbacks is the pith of the
bicategory of spans of `C`. Spelled explicitly, this is a bicategory where
- objects are objects of `C`
- 1-morphisms `c ‚Üí c'` are spans `c ‚Üê x ‚Üí c'` in `C`.
- 2-morphisms are isomorphisms of spans, i.e digrams
  ```
      c
    ‚Üó ‚Üë
  x ‚Üí y
    ‚Üò ‚Üì
      c'
  ```
  where the horizontal morphism is an isomorphism.

By construction, this is a locally groupoidal bicategory.
Please refer to the terminological note in the module docstring
to have an explanation for the name of this object.
-/
abbrev EffBurnside (C : Type*) [Category* C] [Limits.HasPullbacks C] :=
    Bicategory.Pith (Spans C ‚ä§ ‚ä§)

namespace EffBurnside

open Bicategory

-- TODO move somewhere else
instance (C : Type*) [Bicategory C] [IsLocallyDiscrete C] :
  IsLocallyGroupoid C := fun x y ‚Ü¶
    ‚ü®fun {a b} f ‚Ü¶ by
      obtain rfl : a = b := IsDiscrete.eq_of_hom f
      obtain rfl : f = ùüô _ := by subsingleton
      infer_instance‚ü©

-- TODO move this elsewhere
@[simp]
lemma _root_.CategoryTheory.Bicategory.Pith.id_iso (C : Type*) [Bicategory C] {x y : Pith C}
    (f : x ‚ü∂ y) :
    (ùüô f : f ‚ü∂ f).iso = .refl _ :=
  rfl

variable (C : Type*) [Category* C] [Limits.HasPullbacks C]

/-- The right inclusion of `C` in `EffBurnside C`. -/
noncomputable def inr : (LocallyDiscrete C) ‚•§·µñ (EffBurnside C) :=
   Bicategory.Pith.pseudofunctorToPith <| Spans.inr C

/-- The left inclusion of `C·µí·µñ` in `EffBurnside C`. -/
noncomputable def inl : (LocallyDiscrete C·µí·µñ) ‚•§·µñ (EffBurnside C) :=
   Bicategory.Pith.pseudofunctorToPith <| Spans.inl C

section

universe w v u

@[local ext]
lemma _root_.CategoryTheory.Bicategory.Opposite.unop2_hom_ext {B : Type u} [Bicategory.{w, v} B]
    {a b : B·µí·µñ} {f g : a ‚ü∂ b} {œÜ œÜ' : f ‚ü∂ g} (h : œÜ.unop2 = œÜ'.unop2) :
    œÜ = œÜ' := by
  cases œÜ
  cases œÜ'
  grind

open Opposite Bicategory.Opposite

/-- The canonical equivalence (in fact, isomorphism) of categories between
`a ‚ü∂ b` and `op b ‚ü∂ op a`,. -/
def _root_.CategoryTheory.Bicategory.Opposite.homCategoryEquivalence
    {B : Type u} [Bicategory.{w, v} B]
    (a b : B) : (op a ‚ü∂ op b) ‚âå (b ‚ü∂ a) where
  functor.obj f := f.unop
  functor.map {f g} Œ∑ := Œ∑.unop2
  inverse.obj f := op f
  inverse.map {f g} Œ∑ := Bicategory.Opposite.op2 Œ∑
  unitIso := NatIso.ofComponents (fun _ ‚Ü¶ .refl _)
  counitIso := NatIso.ofComponents (fun _ ‚Ü¶ .refl _)

end

-- TODO this should be generalized to more general categories of spans as well
attribute [local ext] _root_.CategoryTheory.Bicategory.Opposite.unop2_hom_ext in
open Opposite Bicategory.Opposite in
/-- The "self-duality" of `EffBurnside C`: it sends a span `x ‚Üê c ‚Üí y` to
`y ‚Üê c ‚Üí x` . -/
@[simps!]
noncomputable def duality : (EffBurnside C) ‚•§·µñ (EffBurnside C)·µí·µñ where
  obj J := op J
  map {X Y} f := Quiver.Hom.op <|
    .mk
      { apex := f.of.apex
        l := f.of.r
        r := f.of.l
        wl := by tauto
        wr := by tauto  }
  map‚ÇÇ {X Y} {f g} Œ∑ :=
    Bicategory.Opposite.op2 <|
      .mk <| Spans.mkIso‚ÇÇ
        { hom := Œ∑.iso.hom.hom
          inv := Œ∑.iso.inv.hom }
  mapId x := Iso.op2 <| Core.isoMk <| Spans.mkIso‚ÇÇ (.refl _)
  mapComp {_ _ _} f g := Iso.op2 <| Core.isoMk <| Spans.mkIso‚ÇÇ
    { hom := Spans.compLiftApex (Spans.œÄ·µ£ _ _) (Spans.œÄ‚Çó _ _)
      inv := Spans.compLiftApex (Spans.œÄ·µ£ _ _) (Spans.œÄ‚Çó _ _)
        (by simpa using (Spans.comp_comm _ _).symm)
      hom_inv_id := by dsimp; ext <;> simp
      inv_hom_id := by dsimp; ext <;> simp }
  map‚ÇÇ_whisker_left := by intros; dsimp; ext; dsimp; ext <;> simp
  map‚ÇÇ_whisker_right := by intros; dsimp; ext; dsimp; ext <;> simp
  map‚ÇÇ_associator := by intros; dsimp; ext; dsimp; ext <;> simp
  map‚ÇÇ_left_unitor := by intros; dsimp; ext; simp
  map‚ÇÇ_right_unitor := by intros; dsimp; ext; simp

end EffBurnside

end CategoryTheory
