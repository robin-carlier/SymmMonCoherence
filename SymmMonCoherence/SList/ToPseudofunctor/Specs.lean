/-
Copyright (c) 2026 Robin Carlier. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Robin Carlier
-/
module

public import SymmMonCoherence.SList.ToPseudofunctor.Defs

/-! # Spans of finite types and the Kleisli bicategory of
symmetric lists.

In this file, we use the results from the file
`SymmMonCoherence.Spans.PseudoFromBurnside.Pseudofunctor` to construct a pseudofunctor
from `Burnside (FintypeCat)` to the bicategory `SList.Kleisliáµ’áµ–`.-/

universe v' u'

@[expose] public section

namespace CategoryTheory.SList

variable (C : Type u') [Category.{v'} C] [MonoidalCategory C] [SymmetricCategory C]
section
open Bicategory

instance (J : EffBurnsideFintype.{0}) :
    MonoidalCategory ((EffBurnside.pseudoOfSymmMonCat C).obj J) :=
  inferInstanceAs <| MonoidalCategory (J.as.of â†’ C)

instance (J : EffBurnsideFintype.{0}) :
    SymmetricCategory ((EffBurnside.pseudoOfSymmMonCat C).obj J) :=
  inferInstanceAs <| SymmetricCategory (J.as.of â†’ C)

noncomputable instance {J K : EffBurnsideFintype.{0}} (f : J âŸ¶ K) :
    Functor.Braided ((EffBurnside.pseudoOfSymmMonCat C).map f).toFunctor := by
  dsimp [EffBurnside.pseudoOfSymmMonCat, Kleisli.pseudoOfSymmMonCat]
  infer_instance

instance {J K : EffBurnsideFintype.{0}} {f g : J âŸ¶ K} (Î· : f âŸ¶ g) :
    NatTrans.IsMonoidal ((EffBurnside.pseudoOfSymmMonCat C).mapâ‚‚ Î·).toNatTrans := by
  dsimp [EffBurnside.pseudoOfSymmMonCat, Kleisli.pseudoOfSymmMonCat]
  infer_instance

noncomputable def pseudoOfSymmMonCat.unitEquivalence :
    (EffBurnsideFintype.unit.as.of â†’ C) â‰Œ C where
  functor := Pi.eval _ ()
  inverse := Functor.pi' (fun _ â†¦ ðŸ­ C)
  -- Slight defeq abuse of Functor.pi' (fun _ â†¦ X) â‹™ eval i = X i
  unitIso := .refl _
  counitIso := .refl _

instance : (pseudoOfSymmMonCat.unitEquivalence C).functor.Braided :=
  inferInstanceAs (Functor.Braided <| Pi.eval _ ())

instance : (pseudoOfSymmMonCat.unitEquivalence C).inverse.Braided :=
  inferInstanceAs (Functor.Braided <| Functor.pi' (fun _ â†¦ ðŸ­ C))

instance : (pseudoOfSymmMonCat.unitEquivalence C).unitIso.hom.IsMonoidal := by
  unfold pseudoOfSymmMonCat.unitEquivalence
  dsimp only [Iso.refl_hom]
  convert NatTrans.IsMonoidal.id
    (Fâ‚ := ðŸ­ (((EffBurnside.pseudoOfSymmMonCat C).obj EffBurnsideFintype.unit)))
  ext
  Â· simp only [Functor.comp_obj, Pi.eval_obj, Functor.LaxMonoidal.Îµ,
      Functor.map_id, Category.comp_id]
    rfl
  Â· simp only [Functor.comp_obj, Pi.eval_obj, Functor.LaxMonoidal.Î¼,
      Functor.map_id, Category.comp_id]
    rfl

instance : (pseudoOfSymmMonCat.unitEquivalence C).counitIso.hom.IsMonoidal := by
  unfold pseudoOfSymmMonCat.unitEquivalence
  dsimp only [Iso.refl_hom]
  convert NatTrans.IsMonoidal.id
    (Fâ‚ := (Functor.pi' fun x â†¦ ðŸ­ C) â‹™ Pi.eval (fun a â†¦ C) ())
  ext
  Â· simp [Functor.LaxMonoidal.Îµ]
  Â· simp [Functor.LaxMonoidal.Î¼]

noncomputable def pseudoOfSymmMonCat.objEquivalence (J : EffBurnsideFintype.{0}) :
    ((EffBurnside.pseudoOfSymmMonCat C).obj J) â‰Œ (J.as.of â†’ C) :=
  Equivalence.refl

section

variable (J : EffBurnsideFintype.{0})

noncomputable instance : (pseudoOfSymmMonCat.objEquivalence C J).functor.Braided :=
  inferInstanceAs (Functor.Braided <| ðŸ­ _)

noncomputable instance : (pseudoOfSymmMonCat.objEquivalence C J).inverse.Braided :=
  inferInstanceAs (Functor.Braided <| ðŸ­ _)

instance : (pseudoOfSymmMonCat.objEquivalence C J).unitIso.hom.IsMonoidal := by
  unfold pseudoOfSymmMonCat.objEquivalence
  convert NatTrans.IsMonoidal.id
  ext <;> simp [Functor.LaxMonoidal.Îµ, Functor.LaxMonoidal.Î¼, EffBurnside.pseudoOfSymmMonCat,
    EffBurnside.pseudoFunctorCore]

instance : (pseudoOfSymmMonCat.objEquivalence C J).counitIso.hom.IsMonoidal := by
  unfold pseudoOfSymmMonCat.objEquivalence
  convert NatTrans.IsMonoidal.id
  ext <;> simp [Functor.LaxMonoidal.Îµ, Functor.LaxMonoidal.Î¼, EffBurnside.pseudoOfSymmMonCat,
    EffBurnside.pseudoFunctorCore]

end

section
-- identifying the action of the pseudofunctor with the tensor product

noncomputable abbrev univFinâ‚‚Span :
    (.mk <| .mk <| .of (Fin 2)) âŸ¶ (EffBurnsideFintype.unit) :=
  (EffBurnside.inr (FintypeCat.{0})).map <|
    (FintypeCat.homMk <| (fun _ â†¦ .unit) : (FintypeCat.of <| Fin 2) âŸ¶ (FintypeCat.of Unit)).toLoc
end

-- Note that because of linearity, the isomorphism is necessarily unique
open MonoidalCategory
noncomputable def univFinâ‚‚SpanPushforwardIso :
    (EffBurnside.pushforwardAux univFinâ‚‚Span.of.r () : SList (Fin 2)) â‰… [0]~ âŠ— [1]~ :=
  SList.isoOfMultisetEq _ _ <| by
    simp only [EffBurnside.pushforwardAux, duality_obj_multiset, multiset_singleton,
      Multiset.nodup_singleton, Multiset.mem_singleton, Multiset.count_eq_one_of_mem,
      Multiset.replicate_succ, Multiset.replicate_zero, Multiset.cons_zero,
      Finset.sum_multiset_singleton, Fin.isValue, multiset_tensor, Multiset.singleton_add]
    rfl

/- Via a symmetric monoidal equivalence (Fin 2 â†’ C) â‰Œ C Ã— C, we could make the following assignment
natural, monoidal etc. Really, the thing should be broken down into the part relevant from Kleisli,
and the part coming from EffBurnsideFintype. -/
noncomputable example (X : Fin 2 â†’ C) : ((pseudoOfSymmMonCat.objEquivalence ..).inverse â‹™
    ((EffBurnside.pseudoOfSymmMonCat C).map univFinâ‚‚Span).toFunctor â‹™
    (pseudoOfSymmMonCat.objEquivalence ..).functor â‹™
    (pseudoOfSymmMonCat.unitEquivalence ..).functor).obj X â‰…
    X 0 âŠ— X 1 := by
  let eâ‚€ :=
    (monoidalLift X).mapIso ((monoidalLift (RelativePseudomonad.Î¹ (Fin 2))).mapIso
      univFinâ‚‚SpanPushforwardIso)
  refine eâ‚€ â‰ªâ‰« ?_
  refine (Functor.Monoidal.Î¼Iso
    (monoidalLift (RelativePseudomonad.Î¹ (Fin 2)) â‹™
      (monoidalLift X)) _ _).symm â‰ªâ‰« ?_
  let e (i : Fin 2) :
      (monoidalLift (RelativePseudomonad.Î¹ (Fin 2)) â‹™ monoidalLift X).obj [i]~ â‰… X i :=
    (monoidalLift X).mapIso (monoidalLiftConsNilIso _ i) â‰ªâ‰« monoidalLiftConsNilIso X i
  exact tensorIso (e 0) (e 1)

end

end CategoryTheory.SList

end
