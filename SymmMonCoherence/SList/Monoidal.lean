/-
Copyright (c) 2025 Robin Carlier. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Robin Carlier
-/
module

public import SymmMonCoherence.SList.Relations -- Is it really needed?
public import Mathlib.CategoryTheory.Monoidal.Braided.Basic

/-! # Symmetric monoidal structure on SList

In this file, we construct a symmetric monoidal category structure on the category
of symmetric lists.

-/

universe u

namespace CategoryTheory.SList

open FreeSListQuiv

variable {C : Type u}

section append

/-- Setup a notation for "singleton" symmetric lists. -/
notation3 "["c"]~" => c ::~ []~

/-- The `RecursiveFunctorData` that defines the bifunctor on SList C that append a
symmetric lists to an other. -/
abbrev appendRecData : RecursiveFunctorData C (SList C ‚•§ SList C) where
  nilObj := ùü≠ (SList C)
  consF c := Functor.whiskeringRight _ _ _|>.obj (c>~)
  swapIso x y := NatIso.ofComponents
    (fun F ‚Ü¶
      (Functor.associator ..) ‚â™‚â´
        (Functor.isoWhiskerLeft F <| swapNatIso _ _) ‚â™‚â´
        (Functor.associator ..).symm)
    (fun {x y} f ‚Ü¶ by ext; simp [swap_natural])
  swap_inv := by intros; ext; simp
  hexagon := by intros; ext; simp [swap_hexagon]

public def appendFunctor : SList C ‚•§ (SList C ‚•§ SList C) :=
  appendRecData.functor

public lemma append_nil_eq : appendFunctor.obj (nil : SList C) = ùü≠ (SList C) :=
    RecursiveFunctorData.functor_obj_nil _

public lemma append_cons_eq (x : C) (l : SList C) :
    appendFunctor.obj ((cons x).obj l) = appendFunctor.obj l ‚ãô cons x :=
  RecursiveFunctorData.functor_obj_cons _ _ _

@[expose] public def appendNilIso : appendFunctor.obj []~ ‚âÖ ùü≠ (SList C) := eqToIso append_nil_eq

@[expose] public def appendConsIso (x : C) (l : SList C) :
    appendFunctor.obj ((x>~).obj l) ‚âÖ appendFunctor.obj l ‚ãô cons x :=
  eqToIso <| append_cons_eq x l

public lemma append_nil_map {l l' : SList C} (f : l ‚ü∂ l') :
    (appendFunctor.obj []~).map f =
      appendNilIso.hom.app l ‚â´ f ‚â´ appendNilIso.inv.app l' := by
  simpa [appendNilIso] using Functor.congr_hom (append_nil_eq) f

public lemma append_map_cons_map (x : C) {l l' : SList C} (f : l ‚ü∂ l') :
    appendFunctor.map ((cons x).map f) =
      (appendConsIso _ _).hom ‚â´
      Functor.whiskerRight (appendFunctor.map f) (x>~) ‚â´
      (appendConsIso _ _).inv :=
  RecursiveFunctorData.functor_map_cons_map _ _ _

public lemma append_map_swap (x y : C) (l : SList C) :
    appendFunctor.map (Œ≤~ x y l) =
    (appendConsIso  _ _).hom ‚â´
      (Functor.whiskeringRight _ _ _|>.obj (x>~)).map (appendConsIso _ _).hom ‚â´
      (Functor.associator ..).hom ‚â´
      (Functor.whiskerLeft (appendFunctor.obj l) <| swapNatTrans x y) ‚â´
      (Functor.associator ..).inv ‚â´
      (Functor.whiskeringRight _ _ _|>.obj (y>~)).map (appendConsIso _ _).inv ‚â´
      (appendConsIso _ _).inv := by
  simpa [-RecursiveFunctorData.functor_map_swap] using appendRecData.functor_map_swap x y l

public lemma append_map_swap_app (x y : C) (l l' : SList C) :
    (appendFunctor.map (Œ≤~ x y l)).app l' =
    (appendConsIso x (y ::~ l)).hom.app l' ‚â´
      (x>~.map ((appendConsIso y l).hom.app l')) ‚â´
      Œ≤~ x y (appendFunctor.obj l |>.obj l') ‚â´
      (y>~.map ((appendConsIso x l).inv.app l')) ‚â´
      (appendConsIso y (x ::~ l)).inv.app l' := by
  simpa using congr($(append_map_swap x y l).app l')

@[simp, grind =]
public lemma append_toList (l l' : SList C) :
    ((appendFunctor.obj l).obj l').toList = l.toList ++ l'.toList := by
  cases l with |_ l
  cases l with |_ l
  induction l with
  | nil => simp [‚Üê nil_def, append_nil_eq]
  | cons head tail h => simpa [FreeSListQuiv.cons_obj_eq, SList.œÄ_obj_cons, append_cons_eq] using h

@[simp]
public lemma append_length (x y : SList C) :
    ((appendFunctor.obj x).obj y).length = x.length + y.length := by
  simp [length]

end append

section

local notation3 x " ++~ " y => (appendFunctor.obj x).obj y
local notation3 x " ++~‚Çò " f => (appendFunctor.obj x).map f
local notation3 f " ‚Çò++~ " y => (appendFunctor.map f).app y

-- Need to expose it if we want to use somewhere that the monoidal structure is strict
@[expose] public def associator (x y z : SList C) :
    ((x ++~ y) ++~ z) ‚âÖ x ++~ (y ++~ z) :=
  eqToIso <| by
    apply injective_toList
    grind

@[expose] public def leftUnitor (x : SList C) : ([]~ ++~ x) ‚âÖ x :=
  appendNilIso.app x

@[expose] public def rightUnitor (x : SList C) : (x ++~ []~) ‚âÖ x :=
  eqToIso <| by
    apply injective_toList
    grind

public instance : MonoidalCategoryStruct (SList C) where
  tensorUnit := []~
  tensorObj x y := x ++~ y
  whiskerLeft x {_ _} f := x ++~‚Çò f
  whiskerRight f x := f ‚Çò++~ x
  associator := associator
  leftUnitor := leftUnitor
  rightUnitor := rightUnitor

open MonoidalCategory

/-- The isomorphim that defines the unit object for symmetric lists. -/
public lemma unit_eq_nil : ùüô_ (SList C) = []~ := rfl

/-- The isomorphim that defines the unit object for symmetric lists. -/
@[expose] public def unitIsoNil : ùüô_ (SList C) ‚âÖ []~ := eqToIso unit_eq_nil

public instance : IsEmpty (Fin (ùüô_ (SList C)).length) := by
  simp only [unit_eq_nil, length_nil]
  infer_instance

/-- The isomorphim that defines the unit object for symmetric lists. -/
public lemma tensorObj_eq_append_obj_obj (x y : SList C) : x ‚äó y = x ++~ y := rfl

/-- The isomorphim that defines the tensor product on symmetric lists.
It is usually a bad idea to try to go through this one: prefer phrasing things
in "monoidal langage". -/
@[expose] public def tensorObjIsoDef (x y : SList C) : x ‚äó y ‚âÖ x ++~ y :=
  eqToIso <| tensorObj_eq_append_obj_obj _ _

public lemma whiskerLeft_eq (x : SList C) {y z : SList C} (f : y ‚ü∂ z) :
    x ‚óÅ f = (tensorObjIsoDef x y).hom ‚â´ (x ++~‚Çò f) ‚â´ (tensorObjIsoDef x z).inv := by
  simp [MonoidalCategoryStruct.whiskerLeft, MonoidalCategoryStruct.tensorObj,
    tensorObjIsoDef]

public lemma whiskerRight_eq {x y : SList C} (f : x ‚ü∂ y) (z : SList C) :
    f ‚ñ∑ z = (tensorObjIsoDef x z).hom ‚â´ (f ‚Çò++~ z) ‚â´ (tensorObjIsoDef y z).inv := by
  simp [MonoidalCategoryStruct.whiskerRight, MonoidalCategoryStruct.tensorObj,
    tensorObjIsoDef]

@[simp, grind =]
public lemma tensorUnit_toList : (ùüô_ (SList C)).toList = [] := by simp [tensorUnit]

@[simp, grind =]
public lemma tensorUnit_length : (ùüô_ (SList C)).length = 0 := by simp [tensorUnit]

@[simp, grind =]
public lemma tensorObj_toList (x y : SList C) : (x ‚äó y).toList = x.toList ++ y.toList :=
  append_toList x y

@[simp, grind =]
public lemma tensorObj_length (x y : SList C) : (x ‚äó y).length = x.length + y.length :=
  append_length x y

public lemma tensorObj_cons_eq (x : C) (l l' : SList C) :
    (x ::~ l) ‚äó l' = x ::~ (l ‚äó l') :=
  Functor.congr_obj (append_cons_eq _ _) l'

@[expose] public def tensorObjConsIso (x : C) (l l' : SList C) :
    (x ::~ l) ‚äó l' ‚âÖ x ::~ (l ‚äó l') :=
  eqToIso <| tensorObj_cons_eq _ _ _

@[local simp, local grind =] lemma whiskerLeft_id (x y : SList C) : x ‚óÅ ùüô y = ùüô _ := by
  simp only [whiskerLeft, Functor.map_id]
  rfl

@[local simp, local grind _=_] lemma whiskerLeft_comp (x : SList C) {y z t : SList C}
    (f : y ‚ü∂ z) (g : z ‚ü∂ t) :
    x ‚óÅ (f ‚â´ g) = x ‚óÅ f ‚â´ x ‚óÅ g := by
  simp [whiskerLeft]

@[local simp, local grind =] lemma id_whiskerRight (x y : SList C) :
    ùüô x ‚ñ∑ y = ùüô _ := by
  simp [whiskerRight]
  rfl

@[local simp, local grind _=_] lemma comp_whiskerRight {x y z : SList C}
    (f : x ‚ü∂ y) (g : y ‚ü∂ z) {t : SList C} :
    (f ‚â´ g) ‚ñ∑ t = f ‚ñ∑ t ‚â´ g ‚ñ∑ t := by
  simp [whiskerRight]

public lemma whiskerLeft_nil {l l' : SList C}
    (f : l ‚ü∂ l') :
    []~ ‚óÅ f =
    unitIsoNil.inv ‚ñ∑ _ ‚â´ (Œª_ _).hom ‚â´
      f ‚â´
      (Œª_ _).inv ‚â´ unitIsoNil.hom ‚ñ∑ _ := by
  simpa [MonoidalCategoryStruct.whiskerLeft, MonoidalCategoryStruct.tensorObj, unit_eq_nil,
    MonoidalCategoryStruct.leftUnitor, leftUnitor, unitIsoNil, appendNilIso,
    tensorObjConsIso, appendConsIso] using Functor.congr_hom append_nil_eq f

public lemma whiskerLeft_cons (x : C) (l : SList C) {l' l'' : SList C}
    (f : l' ‚ü∂ l'') :
    (x ::~ l) ‚óÅ f =
    (tensorObjConsIso _ _ _).hom ‚â´
      (x ::~‚Çò (l ‚óÅ f)) ‚â´
      (tensorObjConsIso _ _ _).inv := by
  simpa [MonoidalCategoryStruct.whiskerLeft, MonoidalCategoryStruct.tensorObj,
    tensorObjConsIso, appendConsIso] using Functor.congr_hom (append_cons_eq _ _) f

public lemma whiskerRight_cons_map (x : C) {l' l'' : SList C}
    (f : l' ‚ü∂ l'') (l : SList C) :
    (x ::~‚Çò f) ‚ñ∑ l =
    (tensorObjConsIso _ _ _).hom ‚â´
      (x ::~‚Çò (f ‚ñ∑ l)) ‚â´
      (tensorObjConsIso _ _ _).inv := by
  simpa [MonoidalCategoryStruct.whiskerRight, MonoidalCategoryStruct.tensorObj,
    tensorObjConsIso, appendConsIso] using congr_app (append_map_cons_map x f) l

public lemma whiskerRight_swap (x y : C) (l : SList C) (l' : SList C) :
    (Œ≤~ x y l) ‚ñ∑ l' =
    (tensorObjConsIso x (y ::~ l) l').hom ‚â´
      (x ::~‚Çò (tensorObjConsIso y l l').hom) ‚â´
      Œ≤~ x y (l ‚äó l') ‚â´
      (y ::~‚Çò (tensorObjConsIso x l l').inv) ‚â´
      (tensorObjConsIso y (x ::~ l) l').inv := by
  simpa [MonoidalCategoryStruct.whiskerRight, MonoidalCategoryStruct.tensorObj,
    tensorObjConsIso, appendConsIso] using append_map_swap_app x y l l'

public lemma rightUnitor_cons (x : C) (l : SList C) :
    œÅ_ (x ::~ l) = tensorObjConsIso x l (ùüô_ _) ‚â™‚â´ ((x>~).mapIso (œÅ_ l)) := by
  ext
  simp [MonoidalCategoryStruct.rightUnitor, tensorObjConsIso, rightUnitor, eqToHom_map]

end

open MonoidalCategory

/-- The function œà is essentially a cast along the equality
`(x ‚äó y).length = x.length + y.length`, followed by the equivalence between
`Fin (n + m)` and `Fin n ‚äï Fin m`, but giving it a name and hiding its
implementation as a cast makes for better automation. -/
@[no_expose] public def Œ® (x y : SList C) :
    Fin x.length ‚äï Fin y.length ‚âÉ Fin (x ‚äó y).length :=
  finSumFinEquiv.trans (Equiv.symm <| finCongr <| tensorObj_length x y)

@[elab_as_elim, cases_eliminator]
public lemma fin_tensor_obj_case {x y : SList C}
    {motive : {x y : SList C} ‚Üí Fin (x ‚äó y).length ‚Üí Prop}
    (inl : ‚àÄ (i : Fin x.length), motive (Œ® x y (.inl i)))
    (inr : ‚àÄ (i : Fin y.length), motive (Œ® x y (.inr i)))
    (i : Fin (x ‚äó y).length) : motive i := by
  obtain ‚ü®i, rfl‚ü© := (Œ® _ _).surjective i
  grind

@[simp, grind =]
public lemma Œ®_apply_val_left (x y : SList C) (i : Fin x.length) :
    (Œ® x y (.inl i)).val = i.val := (rfl)

@[simp, grind =]
public lemma Œ®_apply_val_right (x y : SList C) (i : Fin y.length) :
    (Œ® x y (.inr i)).val = x.length + i.val := (rfl)

attribute [local simp, local grind =]
  whiskerLeft_id whiskerLeft_comp id_whiskerRight comp_whiskerRight

lemma tensorHom_def {x y z t : SList C} (f : x ‚ü∂ y) (g : z ‚ü∂ t) :
    f ‚äó‚Çò g = f ‚ñ∑ _ ‚â´ _ ‚óÅ g  := rfl

public section

@[simp, grind =] private lemma toAinf_app_appendIsoDef (l l' : SList C) :
    toAinf.app (tensorObjIsoDef l l').hom = 1 := by simp [tensorObjIsoDef]

@[simp, grind =]
lemma Ainf.shift_zero (x : Ainf.Group) : Ainf.shift 0 x = x := by
  induction x using Ainf.toCoxeterSystem.simple_induction with
  | one => simp
  | simple i => simp [Ainf.shift_simple]
  | mul w i ih_w ih_i => simp [ih_w, ih_i]

lemma toAinf_whiskerLeft (x : SList C) {y y' : SList C} (f : y ‚ü∂ y') :
    toAinf.app (x ‚óÅ f) = Ainf.shift x.length (toAinf.app f) := by
  induction x using SList.cons_induction with
  | nil => simp [whiskerLeft_eq, append_nil_map, appendNilIso]
  | cons head tail ih =>
    simp [whiskerLeft_cons, tensorObjConsIso, ih, Ainf.shift_shift,
      Nat.add_comm]

lemma toAinf_appendFunctor_map_cons (x : C) {y y' : SList C} (f : y ‚ü∂ y') (z : SList C) :
    toAinf.app ((x ::~‚Çò f) ‚ñ∑ z) = Ainf.shift 1 (toAinf.app (f ‚ñ∑ z)) := by
  simp [whiskerRight_cons_map, tensorObjConsIso]

lemma toAinf_appendFunctor_map_swap (x y : C) (l : SList C) (z : SList C) :
    toAinf.app ((Œ≤~ x y l) ‚ñ∑ z) =
    toAinf.app (Œ≤~ x y (l ‚äó z)) := by
  simp [whiskerRight_swap, tensorObjConsIso]

lemma toAinf_whiskerRight {x x' : SList C} (f : x ‚ü∂ x') (y : SList C) :
    toAinf.app (f ‚ñ∑ y) = toAinf.app f := by
  induction f using SList.hom_induction' with
  | comp f g _ _ => grind
  | id x => grind
  | cons head f _ => grind [toAinf_appendFunctor_map_cons]
  | swap x y l => grind [toAinf_appendFunctor_map_swap]

end

section

lemma toPerm_whiskerLeft_apply_left (x : SList C) {y y' : SList C} (f : y ‚ü∂ y')
    (i : ‚Ñï) (hi : i < x.length) :
    (toPerm.app (x ‚óÅ f)) i = i := by
  simp only [toPerm, weight.postComp_app, toAinf_whiskerLeft]
  exact AinfToPerm_shift_apply_lt _ _ _ hi

lemma toPerm_whiskerLeft_apply_right (x : SList C) {y y' : SList C} (f : y ‚ü∂ y')
    (i : ‚Ñï) :
    (toPerm.app (x ‚óÅ f)) (x.length + i) = x.length + (toPerm.app f) i := by
  simp only [toPerm, weight.postComp_app, toAinf_whiskerLeft]
  rw [AinfToPerm_shift_apply_ge _ _ _ (by simp)]
  simp

lemma toPerm_whiskerRight_apply {x x' : SList C} (f : x ‚ü∂ x') (y : SList C) (i : ‚Ñï) :
    (toPerm.app (f ‚ñ∑ y)) i = (toPerm.app f) i := by
  simp [toPerm, toAinf_whiskerRight]

end

@[simp, grind =] public lemma tensorHom_apply_left {x y z t : SList C} (f : x ‚ü∂ y) (g : z ‚ü∂ t)
    (i : Fin y.length) :
    toEquiv (f ‚äó‚Çò g) (Œ® y t <| .inl i) = Œ® _ _ (.inl <| toEquiv f i) := by
  ext
  simp [toEquiv_apply_val, tensorHom_def, toPerm_whiskerRight_apply,
    toPerm_whiskerLeft_apply_left]

@[simp, grind =] public lemma tensorHom_apply_right {x y z t : SList C} (f : x ‚ü∂ y) (g : z ‚ü∂ t)
    (i : Fin t.length) : toEquiv (f ‚äó‚Çò g) (Œ® y t <| .inr i) = Œ® _ _ (.inr <| toEquiv g i) := by
  ext
  simp [toEquiv_apply_val, tensorHom_def, toPerm_whiskerRight_apply,
    toPerm_whiskerLeft_apply_right, toPerm_app_eq_of_lt, length_eq_of_hom f]

lemma id_tensorHom (x : SList C) {y z : SList C} (f : y ‚ü∂ z) :
    ùüô x ‚äó‚Çò f = x ‚óÅ f := by simp [tensorHom_def]

lemma tensorHom_id {x y : SList C} (f : x ‚ü∂ y) (z : SList C) :
     f ‚äó‚Çò (ùüô z) = f ‚ñ∑ z := by
  simp [tensorHom_def]

@[simp, grind =] public lemma whiskerLeft_apply_right (x : SList C) {y z : SList C} (f : y ‚ü∂ z)
    (i : Fin z.length) :
    toEquiv (x ‚óÅ f) (Œ® x z <| .inr i) = (Œ® _ _) (.inr (toEquiv f i)) := by
  ext
  simp [‚Üê id_tensorHom]

@[simp, grind =] public lemma whiskerLeft_apply_left (x : SList C) {y z : SList C} (f : y ‚ü∂ z)
    (i : Fin x.length) :
    toEquiv (x ‚óÅ f) (Œ® x z <| .inl i) = Œ® _ _ (.inl i) := by
  ext
  simp [‚Üê id_tensorHom]

@[simp, grind =] public lemma whiskerRight_apply_left {x y : SList C} (f : x ‚ü∂ y) (z : SList C)
    (i : Fin z.length) :
    toEquiv (f ‚ñ∑ z) (Œ® y z <| .inr i) = Œ® _ _ (.inr i) := by
  ext
  simp [‚Üê tensorHom_id]

@[simp, grind =] public lemma whiskerRight_apply_right {x y : SList C} (f : x ‚ü∂ y) (z : SList C)
    (i : Fin y.length) :
    toEquiv (f ‚ñ∑ z) (Œ® y z <| .inl i) = Œ® _ _ (.inl (toEquiv f i)) := by
  ext
  simp [‚Üê tensorHom_id]

@[simp, grind =] public lemma associator_hom_left_left (x y z : SList C) (i : Fin x.length) :
    toEquiv (Œ±_ x y z).hom (Œ® x (y ‚äó z) <| .inl i) = Œ® (x ‚äó y) z (.inl <| Œ® x y <| .inl i) := by
  ext
  simp [MonoidalCategoryStruct.associator, associator, Œ®]

@[simp, grind =] public lemma associator_hom_right_left (x y z : SList C) (i : Fin y.length) :
    toEquiv (Œ±_ x y z).hom (Œ® x (y ‚äó z) <| .inr <| Œ® y z <| .inl i) =
      Œ® (x ‚äó y) z (.inl <| Œ® x y <| .inr i) := by
  ext
  simp [MonoidalCategoryStruct.associator, associator, Œ®]

@[simp, grind =] public lemma associator_hom_right_right (x y z : SList C) (i : Fin z.length) :
    toEquiv (Œ±_ x y z).hom (Œ® x (y ‚äó z) <| .inr <| Œ® y z <| .inr i) = Œ® (x ‚äó y) z (.inr i) := by
  ext
  simp [MonoidalCategoryStruct.associator, associator, Œ®, Nat.add_assoc]

@[simp, grind =] public lemma associator_inv_left_left (x y z : SList C) (i : Fin x.length) :
    toEquiv (Œ±_ x y z).inv ((Œ® (x ‚äó y) z) (.inl <| Œ® x y <| .inl i)) = Œ® x (y ‚äó z) (.inl i) := by
  ext
  simp [MonoidalCategoryStruct.associator, associator, Œ®]

@[simp, grind =] public lemma associator_inv_left_right (x y z : SList C) (i : Fin y.length) :
    toEquiv (Œ±_ x y z).inv (Œ® (x ‚äó y) z (.inl <| Œ® x y <| .inr i)) =
      Œ® x (y ‚äó z) (.inr <| Œ® y z <| .inl i) := by
  ext
  simp [MonoidalCategoryStruct.associator, associator, Œ®]

@[simp, grind =] public lemma associator_inv_right (x y z : SList C) (i : Fin z.length) :
    toEquiv (Œ±_ x y z).inv (Œ® (x ‚äó y) z <| .inr i) = Œ® x (y ‚äó z) (.inr <| Œ® y z <| .inr i) := by
  ext
  simp [MonoidalCategoryStruct.associator, associator, Œ®, Nat.add_assoc]

@[simp] public lemma toEquiv_leftUnitor_hom (x : SList C) (i : Fin x.length) :
    toEquiv (Œª_ x).hom i = Œ® (ùüô_ (SList C)) x (.inr <| i) := by
  ext
  simp [MonoidalCategoryStruct.leftUnitor, leftUnitor, appendNilIso]

@[simp] public lemma toEquiv_leftUnitor_inv (x : SList C) (i : Fin x.length) :
    toEquiv (Œª_ x).inv (Œ® (ùüô_ (SList C)) x <| .inr i) = i := by
  ext
  simp [MonoidalCategoryStruct.leftUnitor, leftUnitor, appendNilIso]

@[simp] public lemma toEquiv_rightUnitor_hom (x : SList C) (i : Fin x.length) :
    toEquiv (œÅ_ x).hom i = Œ® _ _ (.inl i) := by
  ext
  simp [MonoidalCategoryStruct.rightUnitor, rightUnitor]

@[simp] public lemma toEquiv_rightUnitor_inv (x : SList C) (i : Fin x.length) :
    toEquiv (œÅ_ x).inv (Œ® _ _ (.inl i)) = i := by
  ext
  simp [MonoidalCategoryStruct.rightUnitor, rightUnitor]

lemma associator_naturality {x x' y y' z z' : SList C}
    (f : x ‚ü∂ x') (g : y ‚ü∂ y') (h : z ‚ü∂ z') :
    ((f ‚äó‚Çò g) ‚äó‚Çò h) ‚â´ (Œ±_ x' y' z').hom =
    (Œ±_ x y z).hom ‚â´ (f ‚äó‚Çò g ‚äó‚Çò h) := by
  rw [hom_eq_iff_toEquiv_eq]
  ext i
  obtain ‚ü®i, rfl‚ü© := (Œ® _ _).surjective i
  cases i with
  | inl i => simp
  | inr i =>
    obtain ‚ü®i, rfl‚ü© := (Œ® _ _).surjective i
    cases i with simp

lemma pentagon_identity (w x y z : SList C) :
    (Œ±_ w x y).hom ‚ñ∑ z ‚â´ (Œ±_ w (x ‚äó y) z).hom ‚â´ w ‚óÅ (Œ±_ x y z).hom =
    (Œ±_ (w ‚äó x) y z).hom ‚â´ (Œ±_ w x (y ‚äó z)).hom := by
  rw [hom_eq_iff_toEquiv_eq]
  ext i
  obtain ‚ü®i, rfl‚ü© := (Œ® _ _).surjective i
  cases i with
  | inl i => simp
  | inr i =>
    obtain ‚ü®i, rfl‚ü© := (Œ® _ _).surjective i
    cases i with
      | inl i => simp
      | inr i =>
        obtain ‚ü®i, rfl‚ü© := (Œ® _ _).surjective i
        cases i with simp

public instance : MonoidalCategory (SList C) where
  tensorHom_def f g := rfl
  id_tensorHom_id _ _ := by
    rw [hom_eq_iff_toEquiv_eq]
    ext i
    simp [tensorHom_def]
  associator_naturality f g h := by simpa using associator_naturality f g h
  leftUnitor_naturality f := by
    rw [hom_eq_iff_toEquiv_eq]
    ext i
    simp
  tensorHom_comp_tensorHom _ _ _ _ := by simp [tensorHom]
  rightUnitor_naturality f := by
    rw [hom_eq_iff_toEquiv_eq]
    ext j
    simp
  pentagon x y z t := by simpa using pentagon_identity x y z t
  triangle u v := by
    rw [hom_eq_iff_toEquiv_eq]
    ext j : 1
    obtain ‚ü®j, rfl‚ü© := (Œ® _ _).surjective j
    cases j with simp


/-- Giving a name to the isomorphism that translates between `SList.cons` and the
tensor product with a singleton SList. -/
@[expose, simps! -isSimp] public def consTensSingletonIso (x : C) (l : SList C) :
    (x ::~ l) ‚âÖ [x]~ ‚äó l :=
  Iso.symm <|
    tensorObjConsIso x []~ l ‚â™‚â´ (x>~).mapIso (whiskerRightIso unitIsoNil.symm _ ‚â™‚â´ Œª_ _)

public lemma consTensSingletonIso_hom_naturality (x : C) {l l' : SList C} (f : l ‚ü∂ l') :
    (consTensSingletonIso x l).hom ‚â´ [x]~ ‚óÅ f =
      (x ::~‚Çò f) ‚â´ (consTensSingletonIso x l').hom := by
  simp only [consTensSingletonIso_hom, whiskerLeft_cons, Category.assoc, Iso.inv_hom_id_assoc]
  simp_rw [‚Üê Functor.map_comp_assoc, ‚Üê whisker_exchange, leftUnitor_inv_naturality_assoc]

public lemma consTensSingletonIso_inv_naturality (x : C) {l l' : SList C} (f : l ‚ü∂ l') :
    (consTensSingletonIso x l).inv ‚â´ (x ::~‚Çò f) =
      [x]~ ‚óÅ f ‚â´ (consTensSingletonIso x l').inv := by
  simp only [Iso.eq_comp_inv, Category.assoc, ‚Üê consTensSingletonIso_hom_naturality]
  simp

/-! Now that we have the monoidal structure, the rest of this file is devoted to extend it to a
symmetric monoidal structure. The construction follows Piceghello‚Äôs thesis (chapter 4). -/

section Q
open MonoidalCategory

@[reassoc (attr := local simp)]
private lemma cons_cons_hom_inv_id (u v : C) {l l' : SList C} (e : l ‚âÖ l') :
  (u ::~‚Çò (v ::~‚Çò e.hom)) ‚â´ (u ::~‚Çò (v ::~‚Çò e.inv)) = ùüô _ := by simp [‚Üê Functor.map_comp]

@[reassoc (attr := local simp)]
private lemma cons_cons_inv_hom_id (u v : C) {l l' : SList C} (e : l ‚âÖ l') :
  (u ::~‚Çò (v ::~‚Çò e.inv)) ‚â´ (u ::~‚Çò (v ::~‚Çò e.hom)) = ùüô _ := by simp [‚Üê Functor.map_comp]

/--
The natural isomorphism `Q_{x, l‚ÇÇ}(l‚ÇÅ) : (x :: l‚ÇÅ) ‚äó l‚ÇÇ ‚âÖ l‚ÇÅ ‚äó (x :: l‚ÇÇ)`.
This corresponds to Lemma 4.25 in Piceghello's thesis.
This is an auxiliary isomorphism for the definition of the general braiding isomorphism.
-/
public def Q (x : C) (l‚ÇÇ : SList C) :
    (x>~) ‚ãô tensorRight l‚ÇÇ ‚âÖ tensorRight (x ::~ l‚ÇÇ) :=
  recNatIso
    (tensorObjConsIso x []~ l‚ÇÇ ‚â™‚â´
      (x>~).mapIso (whiskerRightIso unitIsoNil.symm l‚ÇÇ) ‚â™‚â´
      (x>~).mapIso (Œª_ l‚ÇÇ) ‚â™‚â´
      (Œª_ (x ::~ l‚ÇÇ)).symm ‚â™‚â´
      whiskerRightIso unitIsoNil _)
    (fun y l‚ÇÅ prev =>
      -- Inductive step: l‚ÇÅ = y :: l‚ÇÅ
      -- (x ::~ y ::~ l‚ÇÅ) ‚äó l‚ÇÇ ‚âÖ (x ::~ l‚ÇÇ) ‚äó y ::~ l‚ÇÅ
      tensorObjConsIso x (y ::~ l‚ÇÅ) l‚ÇÇ ‚â™‚â´
        (x>~).mapIso (tensorObjConsIso y l‚ÇÅ l‚ÇÇ) ‚â™‚â´
        swapIso x y (l‚ÇÅ ‚äó l‚ÇÇ) ‚â™‚â´
        (y>~).mapIso (tensorObjConsIso x l‚ÇÅ l‚ÇÇ).symm ‚â™‚â´
        (y>~).mapIso prev ‚â™‚â´
        (tensorObjConsIso y l‚ÇÅ (x ::~ l‚ÇÇ)).symm)
    (fun y z l‚ÇÅ prev => by
      dsimp at prev ‚ä¢
      simp [whiskerRight_cons_map, whiskerRight_swap, reassoc_of%
        SList.swap_natural x z (tensorObjConsIso y l‚ÇÅ l‚ÇÇ).hom,
        ‚Üê reassoc_of% SList.swap_hexagon x y z (l‚ÇÅ ‚äó l‚ÇÇ),
        ‚Üê reassoc_of% SList.swap_natural x y (tensorObjConsIso z l‚ÇÅ l‚ÇÇ).hom,
        ‚Üê reassoc_of% SList.swap_natural y z prev.hom,
        ‚Üê reassoc_of% SList.swap_natural y z (tensorObjConsIso x l‚ÇÅ l‚ÇÇ).inv])
    (fun c l l' f h h' e => by
      dsimp at h h' e ‚ä¢
      simp only [whiskerRight_cons_map, Functor.map_comp, Category.assoc, Iso.inv_hom_id_assoc,
        Iso.map_inv_hom_id_assoc, ‚Üê reassoc_of% swap_natural x c (f ‚ñ∑ l‚ÇÇ), Iso.cancel_iso_hom_left]
      simp_rw [‚Üê Functor.map_comp_assoc, ‚Üê e]
      simp [whiskerRight_cons_map])

@[local simp, reassoc]
public lemma Q_hom_app_nil (x : C) (l‚ÇÇ : SList C) :
    (Q x l‚ÇÇ).hom.app []~ =
      (tensorObjConsIso x []~ l‚ÇÇ).hom ‚â´
      (x>~).map (unitIsoNil.inv ‚ñ∑ l‚ÇÇ) ‚â´
      (x>~).map (Œª_ l‚ÇÇ).hom ‚â´
      (Œª_ (x ::~ l‚ÇÇ)).inv ‚â´
      unitIsoNil.hom ‚ñ∑ _ := by
  simp [Q]

@[local simp, reassoc]
public lemma Q_hom_app_cons (x : C) (l‚ÇÇ : SList C) (y : C) (l‚ÇÅ : SList C) :
    (Q x l‚ÇÇ).hom.app (y ::~ l‚ÇÅ) =
      (tensorObjConsIso x (y ::~ l‚ÇÅ) l‚ÇÇ).hom ‚â´
        (x>~).map (tensorObjConsIso y l‚ÇÅ l‚ÇÇ).hom ‚â´
        Œ≤~ x y (l‚ÇÅ ‚äó l‚ÇÇ) ‚â´
        (y>~).map (tensorObjConsIso x l‚ÇÅ l‚ÇÇ).inv ‚â´
        (y>~).map ((Q x l‚ÇÇ).hom.app l‚ÇÅ) ‚â´
        (tensorObjConsIso y l‚ÇÅ (x ::~ l‚ÇÇ)).inv := by
  simp [Q]

@[local simp, reassoc]
public lemma Q_inv_app_cons (x : C) (l‚ÇÇ : SList C) (y : C) (l‚ÇÅ : SList C) :
    (Q x l‚ÇÇ).inv.app (y ::~ l‚ÇÅ) =
    (tensorObjConsIso y l‚ÇÅ (x ::~ l‚ÇÇ)).hom ‚â´
      (y>~).map ((Q x l‚ÇÇ).inv.app l‚ÇÅ) ‚â´
      (y>~).map (tensorObjConsIso x l‚ÇÅ l‚ÇÇ).hom ‚â´
      Œ≤~ y x (l‚ÇÅ ‚äó l‚ÇÇ) ‚â´
      (x>~).map (tensorObjConsIso y l‚ÇÅ l‚ÇÇ).inv ‚â´
      (tensorObjConsIso x (y ::~ l‚ÇÅ) l‚ÇÇ).inv := by
  rw [‚Üê IsIso.inv_eq_inv]
  simp only [Functor.comp_obj, Functor.flip_obj_obj, curriedTensor_obj_obj, NatIso.inv_inv_app,
    Q_hom_app_cons, IsIso.inv_comp, IsIso.Iso.inv_inv, Category.assoc, IsIso.Iso.inv_hom,
    Iso.cancel_iso_hom_left, IsIso.eq_inv_comp, Iso.map_inv_hom_id_assoc, swap_swap_assoc,
    Iso.map_hom_inv_id_assoc]
  simp [‚Üê Functor.map_comp_assoc]

public lemma Q_hom_app_naturality (x : C) {l‚ÇÇ l‚ÇÇ' : SList C} (f : l‚ÇÇ ‚ü∂ l‚ÇÇ') (l‚ÇÅ : SList C) :
    (x ::~ l‚ÇÅ) ‚óÅ f ‚â´ (Q x l‚ÇÇ').hom.app l‚ÇÅ = (Q x l‚ÇÇ).hom.app l‚ÇÅ ‚â´ l‚ÇÅ ‚óÅ (x ::~‚Çò f) := by
  induction l‚ÇÅ using SList.cons_induction with
  | nil =>
    dsimp
    simp only [whiskerLeft_cons, whiskerLeft_nil, Functor.map_comp, Category.assoc, Q_hom_app_nil,
      Iso.inv_hom_id_assoc, hom_inv_whiskerRight_assoc, Iso.cancel_iso_hom_left]
    simp [‚Üê Functor.map_comp_assoc, -Functor.map_comp]
  | cons c l‚ÇÅ ih =>
    dsimp
    simp only [whiskerLeft_cons, Functor.map_comp, Category.assoc, Q_hom_app_cons,
      Functor.flip_obj_obj, curriedTensor_obj_obj, Iso.inv_hom_id_assoc, Iso.map_inv_hom_id_assoc,
      ‚Üê reassoc_of% swap_natural x c (l‚ÇÅ ‚óÅ f), Iso.cancel_iso_hom_left]
    simp [‚Üê Functor.map_comp_assoc, -Functor.map_comp, ‚Üê ih, whiskerLeft_cons]

/-- This is lemma 4.28 in Piceghello's thesis.
-- TODO draw nice Ascii/Unicode diagram mirroring Figure 4.8 in Piceghello's
-/
lemma Q_cons_Q_cons_swap (x y : C) (l‚ÇÅ l‚ÇÇ : SList C) :
    Œ≤~ x y (l‚ÇÇ ‚äó l‚ÇÅ) ‚â´
      (y ::~‚Çò (tensorObjConsIso x l‚ÇÇ l‚ÇÅ).inv) ‚â´
      (y ::~‚Çò ((Q x l‚ÇÅ).hom.app l‚ÇÇ)) ‚â´
      (tensorObjConsIso y l‚ÇÇ (x ::~ l‚ÇÅ)).inv ‚â´
      ((Q y (x ::~ l‚ÇÅ)).hom.app l‚ÇÇ) =
    (x::~‚Çò (tensorObjConsIso y l‚ÇÇ l‚ÇÅ).inv) ‚â´
      (x ::~‚Çò ((Q y l‚ÇÅ).hom.app l‚ÇÇ)) ‚â´
      (tensorObjConsIso x l‚ÇÇ (y ::~ l‚ÇÅ)).inv ‚â´
      (Q x (y ::~ l‚ÇÅ)).hom.app l‚ÇÇ ‚â´ (l‚ÇÇ ‚óÅ Œ≤~ x y l‚ÇÅ) := by
  induction l‚ÇÇ using SList.cons_induction with
  | nil =>
    simp only [Functor.flip_obj_obj, curriedTensor_obj_obj, Q_hom_app_nil, Functor.map_comp,
      Iso.inv_hom_id_assoc, Category.assoc, Iso.map_inv_hom_id_assoc,
      reassoc_of% swap_natural x y (unitIsoNil.inv ‚ñ∑ l‚ÇÅ), reassoc_of% swap_natural x y (Œª_ l‚ÇÅ).hom,
      whiskerLeft_nil, hom_inv_whiskerRight_assoc]
    simp [‚Üê Functor.map_comp_assoc, -Functor.map_comp]
  | cons z l‚ÇÇ ih =>
    dsimp at ih ‚ä¢
    simp only [Q_hom_app_cons, Functor.flip_obj_obj, curriedTensor_obj_obj, Functor.map_comp,
      Iso.inv_hom_id_assoc, Category.assoc, Iso.map_inv_hom_id_assoc] at ih ‚ä¢
    simp only [whiskerLeft_cons, Iso.inv_hom_id_assoc]
    have := swap_natural y z ((Q x l‚ÇÅ).hom.app l‚ÇÇ)
    dsimp at this
    simp only [‚Üê reassoc_of% this]
    have := swap_natural y z (tensorObjConsIso x l‚ÇÇ l‚ÇÅ).inv
    simp only at this
    simp only [‚Üê reassoc_of% this]
    have := swap_natural x y (tensorObjConsIso z l‚ÇÇ l‚ÇÅ).hom
    simp only [reassoc_of% this, reassoc_of% swap_hexagon]
    have := congr(z::~‚Çò $ih)
    simp only [Functor.map_comp] at this
    simp only [reassoc_of% this]
    have := swap_natural x z ((Q y l‚ÇÅ).hom.app l‚ÇÇ)
    simp only [Functor.comp_obj, Functor.flip_obj_obj, curriedTensor_obj_obj] at this
    simp only [‚Üê reassoc_of% this]
    have := swap_natural x z (tensorObjConsIso y l‚ÇÇ l‚ÇÅ).inv
    simp only at this
    simp [reassoc_of% this]

end Q

section

/--
The new braiding `new_braid l‚ÇÅ l‚ÇÇ : l‚ÇÅ ‚äó l‚ÇÇ ‚âÖ l‚ÇÇ ‚äó l‚ÇÅ`.
This corresponds to Lemma 4.29 in Piceghello's thesis.
-/
public def braidNatIso (l‚ÇÇ : SList C) : tensorRight l‚ÇÇ ‚âÖ tensorLeft l‚ÇÇ :=
  recNatIso
    -- Base case: []~ ‚äó l‚ÇÇ ‚âÖ l‚ÇÇ ‚âÖ l‚ÇÇ ‚äó []~
    (whiskerRightIso unitIsoNil.symm _ ‚â™‚â´ Œª_ l‚ÇÇ ‚â™‚â´ (œÅ_ l‚ÇÇ).symm ‚â™‚â´ whiskerLeftIso _ unitIsoNil)
    -- Inductive step: l‚ÇÅ = x :: l‚ÇÅ
    -- (x ::~ l‚ÇÅ) ‚äó l‚ÇÇ ‚âÖ x ::~ (l‚ÇÅ ‚äó l‚ÇÇ) ‚âÖ x ::~ (l‚ÇÇ ‚äó l‚ÇÅ) ‚âÖ (x ::~ l‚ÇÇ) ‚äó l‚ÇÅ ‚âÖ l‚ÇÇ ‚äó (x ::~ l‚ÇÅ)
    (fun x l‚ÇÅ prev =>
      (tensorObjConsIso x l‚ÇÅ l‚ÇÇ) ‚â™‚â´
        (x>~).mapIso prev ‚â™‚â´
        (tensorObjConsIso x l‚ÇÇ l‚ÇÅ).symm ‚â™‚â´
        (Q x l‚ÇÅ).app l‚ÇÇ)
    (fun y z l‚ÇÅ prev => by
      -- Compatibility with swap, this is essentially Q_cons_Q_cons_swap with a few extra steps
      dsimp
      have := Q_cons_Q_cons_swap y z l‚ÇÅ l‚ÇÇ
      simp at this ‚ä¢
      simp [‚Üê this]
      have := swap_natural y z prev.hom
      simp at this
      simp [‚Üê reassoc_of% this, whiskerRight_swap])
    (fun c l l' f h h' e => by
      dsimp at h h' e ‚ä¢
      have := congr(c ::~‚Çò $e)
      simp only [Functor.map_comp] at this
      simp [whiskerRight_cons_map, reassoc_of% this, ‚Üê Q_hom_app_naturality c f l‚ÇÇ,
        whiskerLeft_cons])

lemma braidNatIso_hom_app_nil (l‚ÇÅ : SList C) :
    ((braidNatIso l‚ÇÅ).hom.app []~) =
      unitIsoNil.inv ‚ñ∑ _ ‚â´ (Œª_ l‚ÇÅ).hom ‚â´ (œÅ_ l‚ÇÅ).inv ‚â´
      _ ‚óÅ unitIsoNil.hom := by
  simp [braidNatIso]

lemma braidNatIso_hom_cons (x : C) (l‚ÇÅ l‚ÇÇ : SList C) :
    (braidNatIso l‚ÇÅ).hom.app (x ::~ l‚ÇÇ) =
    (tensorObjConsIso x l‚ÇÇ l‚ÇÅ).hom ‚â´
      (x>~).map ((braidNatIso l‚ÇÅ).hom.app l‚ÇÇ) ‚â´
      (tensorObjConsIso x l‚ÇÅ l‚ÇÇ).inv ‚â´
      (Q x l‚ÇÇ).hom.app l‚ÇÅ := by
  simp [braidNatIso]

end

section
variable {l‚ÇÅ l‚ÇÇ : SList C}

@[no_expose] public def Œ¶ (x : C) (l : SList C) :
    Unit ‚äï Fin l.length ‚âÉ Fin (x ::~ l).length :=
  Equiv.trans (Equiv.sumCongr finOneEquiv.symm (.refl _))
    (finSumFinEquiv.trans (finCongr (by simp +arith)))

@[elab_as_elim, cases_eliminator]
public lemma fin_cons_obj_case {x : C} {L : SList C}
    {motive : {x : C} ‚Üí {L : SList C} ‚Üí Fin (x ::~ L).length ‚Üí Prop}
    (inl : motive (Œ¶ x L (.inl ())))
    (inr : ‚àÄ (i : Fin L.length), motive (Œ¶ x L (.inr i)))
    (i : Fin (x ::~ L).length) : motive i := by
  obtain ‚ü®i, rfl‚ü© := (Œ¶ _ _).surjective i
  grind

@[simp, grind =]
public lemma Œ¶_apply_val_right (x : C) (l : SList C) (i : Fin l.length) :
    (Œ¶ x l (.inr i)).val = 1 + i.val :=
  (rfl)

@[simp, grind =]
public lemma Œ¶_apply_val_left (x : C) (l : SList C) :
    (Œ¶ x l (.inl ())).val = 0 := (rfl)

@[local simp, grind =]
public lemma toEquiv_cons_map_Œ¶_inr (x : C) {l l' : SList C} (f : l ‚ü∂ l') (i : Fin l'.length) :
    toEquiv (x ::~‚Çò f) (Œ¶ _ _ <| .inr i) = (Œ¶ _ _ <| .inr <| (toEquiv f) i) := by
  ext
  simp [toEquiv, Nat.add_comm, toPerm_app_cons_apply_succ, Œ¶]

@[local simp, grind =]
public lemma toEquiv_cons_map_Œ¶_inl (x : C) {l l' : SList C} (f : l ‚ü∂ l') :
    toEquiv (x ::~‚Çò f) (Œ¶ _ _ <| .inl ()) = (Œ¶ _ _ <| .inl ()) := by
  ext
  simp [toEquiv, Œ¶]

@[local simp]
public lemma toEquiv_swap_Œ¶_inl (x y : C) (l : SList C) :
    toEquiv (Œ≤~ x y l) (Œ¶ _ _ <| .inl ()) = Œ¶ _ _ (.inr <| Œ¶ _ _ <| .inl ()) := by
  ext
  simp [toEquiv, Œ¶]

@[local simp]
public lemma toEquiv_swap_Œ¶_inr_Œ¶_inl (x y : C) (l : SList C) :
    toEquiv (Œ≤~ x y l) (Œ¶ _ _ (.inr <| Œ¶ _ _ <| .inl ())) =
    (Œ¶ _ _ <| .inl ()) := by
  ext
  simp [toEquiv, Œ¶]

@[local simp, grind =]
public lemma toEquiv_swap_Œ¶_inr_Œ¶_inr (x y : C) (l : SList C) (i : Fin l.length) :
    toEquiv (Œ≤~ x y l) (Œ¶ _ _ <| .inr <| Œ¶ _ _ <| .inr i) =
    (Œ¶ _ _ <| .inr <| Œ¶ _ _ <| .inr i) := by
  ext
  grind [toEquiv, Œ¶]

@[local simp, grind =]
public lemma toEquiv_tensorObjConsIso_hom_Œ¶_inl (x : C) (l : SList C) (l' : SList C) :
    toEquiv (tensorObjConsIso x l l').hom (Œ¶ _ _ <| .inl ()) =
    Œ® _ _ (.inl <| Œ¶ _ _ <| .inl ()) := by
  ext
  simp [tensorObjConsIso]

@[local simp, grind =]
public lemma toEquiv_tensorObjConsIso_hom_Œ¶_inl_Œ®_inl
    (x : C) (l : SList C) (l' : SList C) (i : Fin l.length) :
    toEquiv (tensorObjConsIso x l l').hom (Œ¶ _ _ <| .inr <| Œ® _ _ <| .inl i) =
    Œ® _ _ (.inl <| Œ¶ _ _ <| .inr i) := by
  ext
  simp [tensorObjConsIso]

@[local simp]
public lemma toEquiv_tensorObjConsIso_hom_Œ¶_inr
    (x : C) (l : SList C) (l' : SList C) (i : Fin l'.length) :
    toEquiv (tensorObjConsIso x l l').hom (Œ¶ _ _ <| .inr <| Œ® _ _ <| .inr i) =
    Œ® _ _ (.inr i) := by
  ext
  simp +arith [tensorObjConsIso]

@[local simp]
public lemma toEquiv_tensorObjConsIso_inv_Œ¶_inl (x : C) (l : SList C) (l' : SList C) :
    toEquiv (tensorObjConsIso x l l').inv (Œ® _ _ <| .inl <| Œ¶ _ _ <| .inl ()) =
    (Œ¶ _ _ <| .inl ()) := by
  ext
  simp [tensorObjConsIso]

@[local simp]
public lemma toEquiv_tensorObjConsIso_inv_Œ¶_inl_Œ®_inl
    (x : C) (l : SList C) (l' : SList C) (i : Fin l.length) :
    toEquiv (tensorObjConsIso x l l').inv (Œ® _ _ <| .inl <| Œ¶ _ _ <| .inr i) =
    (Œ¶ _ _ <| .inr <| Œ® _ _ <| .inl i) := by
  ext
  simp [tensorObjConsIso]

@[local simp]
public lemma toEquiv_tensorObjConsIso_inv_Œ¶_inr
    (x : C) (l : SList C) (l' : SList C) (i : Fin l'.length) :
    toEquiv (tensorObjConsIso x l l').inv (Œ® _ _ <| .inr i) =
    (Œ¶ _ _ <| .inr <| Œ® _ _ <| .inr i) := by
  ext
  simp +arith [tensorObjConsIso]

@[simp]
public lemma toEquiv_consTensSingletonIso_hom_right (x : C) (l : SList C) (t : Fin l.length) :
    (toEquiv (consTensSingletonIso x l).hom) (Œ® _ _ <| .inr t) = Œ¶ _ _ (.inr t) := by
  simp [consTensSingletonIso]

@[simp]
public lemma toEquiv_consTensSingletonIso_hom_left (x : C) (l : SList C) :
    (toEquiv (consTensSingletonIso x l).hom) (Œ® _ _ (.inl <| Œ¶ _ _ <| .inl ())) =
    (Œ¶ _ _ <| .inl ()) := by
  simp [consTensSingletonIso]

@[simp]
public lemma toEquiv_consTensSingletonIso_inv_right (x : C) (l : SList C) (t : Fin l.length) :
    (toEquiv (consTensSingletonIso x l).inv) (Œ¶ _ _ (.inr t)) = Œ® _ _ (.inr t) := by
  simp [consTensSingletonIso]

@[simp]
public lemma toEquiv_consTensSingletonIso_inv_left (x : C) (l : SList C) :
    (toEquiv (consTensSingletonIso x l).inv) (Œ¶ _ _ <| .inl ()) =
    Œ® _ _ (.inl <| Œ¶ _ _ <| .inl ()) := by
  simp [consTensSingletonIso]

@[simp]
lemma Q_hom_app_left (x : C) (l‚ÇÇ : SList C) (l : SList C) (i : Fin l.length) :
    toEquiv ((Q x l‚ÇÇ).hom.app l) (Œ® _ _ <| .inl i) =
    Œ® _ _ (.inl <| Œ¶ _ _ (.inr i)) := by
  induction l using SList.cons_induction with
  | nil =>
    simp only [length_nil] at i
    exact Fin.elim0 i
  | cons c l ih =>
    obtain ‚ü®i, rfl‚ü© := (Œ¶ _ _).surjective i
    cases i with
    | inr i =>
      have := ih i
      simp only [Functor.comp_obj, Functor.flip_obj_obj, curriedTensor_obj_obj] at this
      simp [Q_hom_app_cons, this]
    | inl i =>
      obtain rfl : i = () := rfl
      letI U :=(Q x l‚ÇÇ).hom.app l
      dsimp at U
      simp [Q_hom_app_cons]

@[simp, grind =]
lemma Q_hom_app_right_Œ¶_inl (x : C) (l‚ÇÇ : SList C) (l : SList C) :
    toEquiv ((Q x l‚ÇÇ).hom.app l) (Œ® _ _ (.inr <| Œ¶ _ _ <| .inl ())) =
    Œ® _ _ (.inl <| Œ¶ _ _ <| .inl ()) := by
  induction l using SList.cons_induction with
  | nil => simp [Q_hom_app_nil]
  | cons c l ih =>
    dsimp at ih
    simp [Q_hom_app_cons, ih]

@[simp, grind =]
lemma Q_hom_app_right_right (x : C) (l‚ÇÇ : SList C) (l : SList C) (i : Fin l‚ÇÇ.length) :
    toEquiv ((Q x l‚ÇÇ).hom.app l) (Œ® _ _ <| .inr <| Œ¶ _ _ <| .inr i) =
    Œ® _ _ (.inr i) := by
  induction l using SList.cons_induction with
  | nil => simp [Q_hom_app_nil]
  | cons c l ih =>
    dsimp at ih
    simp [Q_hom_app_cons, ih]

/-- The "braiding" morphism we constructed corresponds to the equivalence
```
  Fin (l‚ÇÅ ‚äó l‚ÇÇ).length
    _ ‚âÉ Fin (l‚ÇÅ.length + l‚ÇÇ.length)
    _ ‚âÉ Fin (l‚ÇÇ.length + l‚ÇÅ.length)
    _ ‚âÉ Fin ((l‚ÇÇ ‚äó l‚ÇÅ).length)
```. -/
public theorem toEquiv_braidNatIso (l‚ÇÅ l‚ÇÇ : SList C) :
    toEquiv (braidNatIso l‚ÇÅ|>.hom.app l‚ÇÇ) =
    ((Œ® l‚ÇÅ l‚ÇÇ).symm.trans (Equiv.sumComm _ _)).trans (Œ® l‚ÇÇ l‚ÇÅ) := by
  ext i : 1
  obtain ‚ü®i, rfl‚ü© := (Œ® l‚ÇÅ l‚ÇÇ).surjective i
  induction l‚ÇÇ using SList.cons_induction generalizing l‚ÇÅ with
  | nil =>
    cases i with
    | inl i => simp [braidNatIso_hom_app_nil]
    | inr i =>
      simp only [length_nil] at i
      exact Fin.elim0 i
  | cons t l‚ÇÇ ih =>
    cases i with
    | inl i =>
      have e‚ÇÅ := Q_hom_app_left t l‚ÇÇ l‚ÇÅ i
      have e‚ÇÇ := ih l‚ÇÅ (.inl i)
      simp only [Functor.comp_obj, Functor.flip_obj_obj, curriedTensor_obj_obj] at e‚ÇÅ e‚ÇÇ
      simp [braidNatIso_hom_cons, e‚ÇÅ, e‚ÇÇ]
    | inr i =>
      obtain ‚ü®i, rfl‚ü© := (Œ¶ _ _).surjective i
      cases i with
      | inr i =>
        have e‚ÇÅ := Q_hom_app_right_right t l‚ÇÇ l‚ÇÅ i
        have e‚ÇÇ := ih l‚ÇÅ (.inr i)
        simp only [Functor.comp_obj, Functor.flip_obj_obj, curriedTensor_obj_obj] at e‚ÇÅ e‚ÇÇ
        simp [braidNatIso_hom_cons, e‚ÇÅ, e‚ÇÇ]
      | inl =>
        have e‚ÇÅ := Q_hom_app_right_Œ¶_inl t l‚ÇÇ l‚ÇÅ
        simp only [Functor.comp_obj, Functor.flip_obj_obj, curriedTensor_obj_obj] at e‚ÇÅ
        simp [braidNatIso_hom_cons, e‚ÇÅ]

@[expose] public abbrev braid (l‚ÇÅ l‚ÇÇ : SList C) : l‚ÇÅ ‚äó l‚ÇÇ ‚ü∂ l‚ÇÇ ‚äó l‚ÇÅ := (braidNatIso l‚ÇÇ|>.hom.app l‚ÇÅ)

@[simp, grind =]
public lemma toEquiv_braid_Œ®_left (l‚ÇÅ l‚ÇÇ : SList C) (i : Fin l‚ÇÇ.length) :
    (toEquiv (braid l‚ÇÅ l‚ÇÇ)) (Œ® l‚ÇÇ l‚ÇÅ <| .inl i) = (Œ® l‚ÇÅ l‚ÇÇ) (.inr i) := by
  simp [toEquiv_braidNatIso]

@[simp, grind =]
public lemma toEquiv_braid_Œ®_right (l‚ÇÅ l‚ÇÇ : SList C) (i : Fin l‚ÇÅ.length) :
    (toEquiv (braid l‚ÇÅ l‚ÇÇ)) (Œ® l‚ÇÇ l‚ÇÅ <| .inr i) = (Œ® l‚ÇÅ l‚ÇÇ) (.inl i) := by
  simp [toEquiv_braidNatIso]

end

@[simp, grind =]
lemma braid_braid (x y : SList C) : braid x y ‚â´ braid y x = ùüô _ := by
  rw [hom_eq_iff_toEquiv_eq]
  ext i : 1
  obtain ‚ü®i, rfl‚ü© := (Œ® _ _).surjective i
  cases i with simp

@[simps!, expose]
public def braidIso (x y : SList C) :
    x ‚äó y ‚âÖ y ‚äó x where
  hom := braid x y
  inv := braid y x

lemma braid_hexagon_forward (x y z : SList C) :
    (Œ±_ x y z).hom ‚â´ braid x (y ‚äó z) ‚â´ (Œ±_ y z x).hom =
    braid x y ‚ñ∑ z ‚â´ (Œ±_ y x z).hom ‚â´ y ‚óÅ braid x z := by
  rw [hom_eq_iff_toEquiv_eq]
  ext j : 1
  cases j using fin_tensor_obj_case with
  | inl j => simp
  | inr j => grind [(Œ® z x).surjective j]

lemma braid_hexagon_reverse (x y z : SList C) :
    (Œ±_ x y z).inv ‚â´ braid (x ‚äó y) z ‚â´ (Œ±_ z x y).inv =
    (x ‚óÅ braid y z) ‚â´ (Œ±_ x z y).inv ‚â´ (braid x z) ‚ñ∑ y := by
  rw [hom_eq_iff_toEquiv_eq]
  ext i
  cases i using fin_tensor_obj_case with
  | inl i => grind [(Œ® z x).surjective i]
  | inr i => simp

lemma braid_naturality_left {x y z : SList C} (f : x ‚ü∂ y) :
    (f ‚ñ∑ z) ‚â´ braid y z = braid x z ‚â´ (z ‚óÅ f) := by
  rw [hom_eq_iff_toEquiv_eq]
  ext i
  obtain ‚ü®i, rfl‚ü© := (Œ® _ _).surjective i
  cases i with simp

lemma braid_naturality_right {x y z : SList C} (g : y ‚ü∂ z) :
    (x ‚óÅ g) ‚â´ braid x z = braid x y ‚â´ (g ‚ñ∑ x) := by
  rw [hom_eq_iff_toEquiv_eq]
  ext i
  obtain ‚ü®i, rfl‚ü© := (Œ® _ _).surjective i
  cases i with simp

public instance : SymmetricCategory (SList C) where
  braiding x y := braidIso x y
  braiding_naturality_left f z := by simpa using braid_naturality_left f
  braiding_naturality_right {_ _} _ f := by simpa using braid_naturality_right f
  hexagon_forward x y z := by simpa using braid_hexagon_forward x y z
  hexagon_reverse x y z := by simpa using braid_hexagon_reverse x y z

public lemma braiding_hom_app_nil (l‚ÇÅ : SList C) :
    (Œ≤_ l‚ÇÅ []~).hom = (_ ‚óÅ unitIsoNil.inv ‚â´ (œÅ_ _).hom ‚â´ (Œª_ _).inv ‚â´ unitIsoNil.hom ‚ñ∑ _) := by
  rw [‚Üê IsIso.inv_eq_inv, IsIso.Iso.inv_hom]
  simp only [BraidedCategory.braiding, braidIso_inv, braid, IsIso.inv_comp, inv_whiskerRight,
    IsIso.Iso.inv_hom, IsIso.Iso.inv_inv, Category.assoc, inv_whiskerLeft]
  rw [‚Üê IsIso.inv_eq_inv]
  simp [braidNatIso_hom_app_nil]

public lemma braiding_inv_app_nil (l‚ÇÅ : SList C) :
    (Œ≤_ l‚ÇÅ []~).inv = unitIsoNil.inv ‚ñ∑ _ ‚â´ (Œª_ _).hom ‚â´ (œÅ_ _).inv ‚â´ _ ‚óÅ unitIsoNil.hom := by
  simp [BraidedCategory.braiding, braidNatIso_hom_app_nil]

public lemma braiding_hom_cons_right (x : C) (l‚ÇÅ l‚ÇÇ : SList C) :
    (Œ≤_ l‚ÇÅ (x ::~ l‚ÇÇ)).hom =
      (Q x l‚ÇÇ).inv.app l‚ÇÅ ‚â´
      (tensorObjConsIso x l‚ÇÅ l‚ÇÇ).hom ‚â´
      (x>~).map (Œ≤_ l‚ÇÅ l‚ÇÇ).hom ‚â´
      (tensorObjConsIso x l‚ÇÇ l‚ÇÅ).inv := by
  rw [‚Üê IsIso.inv_eq_inv, IsIso.Iso.inv_hom]
  simp only [BraidedCategory.braiding, braidIso_inv, braid, Functor.flip_obj_obj,
    curriedTensor_obj_obj, Functor.comp_obj, braidIso_hom, IsIso.inv_comp, IsIso.Iso.inv_inv,
    IsIso.Iso.inv_hom, Category.assoc, NatIso.inv_inv_app]
  rw [‚Üê IsIso.inv_eq_inv]
  simp [braidNatIso_hom_cons, ‚Üê Functor.map_comp]

public lemma braiding_hom_cons_left (x : C) (l‚ÇÅ l‚ÇÇ : SList C) :
    (Œ≤_ (x ::~ l‚ÇÅ) l‚ÇÇ).hom =
      (tensorObjConsIso x l‚ÇÅ l‚ÇÇ).hom ‚â´
      (x>~).map (Œ≤_ l‚ÇÅ l‚ÇÇ).hom ‚â´
      (tensorObjConsIso x l‚ÇÇ l‚ÇÅ).inv ‚â´
      (Q x l‚ÇÅ).hom.app l‚ÇÇ := by
    rw [SymmetricCategory.braiding_swap_eq_inv_braiding]
    rw [‚Üê IsIso.inv_eq_inv, IsIso.Iso.inv_inv, braiding_hom_cons_right]
    simp_rw [IsIso.inv_comp, IsIso.Iso.inv_inv, ‚Üê Functor.map_inv,
      IsIso.Iso.inv_hom]
    simp [SymmetricCategory.braiding_swap_eq_inv_braiding]

@[simp, grind =]
public lemma toEquiv_braiding_hom_Œ®_left (l‚ÇÅ l‚ÇÇ : SList C) (i : Fin l‚ÇÇ.length) :
    toEquiv (Œ≤_ l‚ÇÅ l‚ÇÇ).hom (Œ® l‚ÇÇ l‚ÇÅ <| .inl i) = Œ® l‚ÇÅ l‚ÇÇ (.inr i) := by
  simp [toEquiv_braidNatIso, BraidedCategory.braiding]

@[simp, grind =]
public lemma toEquiv_braiding_hom_Œ®_right (l‚ÇÅ l‚ÇÇ : SList C) (i : Fin l‚ÇÅ.length) :
    toEquiv (Œ≤_ l‚ÇÅ l‚ÇÇ).hom (Œ® l‚ÇÇ l‚ÇÅ <| .inr i) = Œ® l‚ÇÅ l‚ÇÇ (.inl i) := by
  simp [toEquiv_braidNatIso, BraidedCategory.braiding]

end CategoryTheory.SList
