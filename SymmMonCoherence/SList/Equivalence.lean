/-
Copyright (c) 2025 Robin Carlier. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Robin Carlier
-/
module

public import SymmMonCoherence.FreeSMC
public import SymmMonCoherence.SList.Monoidal
public import Mathlib.Tactic.CategoryTheory.Coherence
public import Mathlib.CategoryTheory.Monoidal.CoherenceLemmas

/-!
# Equivalence between `SList C` and `FreeSymmetricMonoidalCategory C`

This file constructs the equivalence of symmetric monoidal categories between the category of
symmetric lists `SList C` and the free symmetric monoidal category
`FreeSymmetricMonoidalCategory C`. The idea of encoding part of the coherence
theorem in this way is due to Stefano Piceghello, and we mostly follow his
work here.

## Main definitions

* `normalization`: The monoidal functor `FreeSMC C ‚•§ SList C` that flattens a tree into a list.
* `inclusion`: The monoidal functor `SList C ‚•§ FreeSMC C`
  that maps a list to a right-associated tree.
* `equivalence`: The equivalence of categories `SList C ‚âå FreeSMC C`.

-/


open CategoryTheory MonoidalCategory FreeSymmetricMonoidalCategory

namespace CategoryTheory.SList

variable (C : Type*)

noncomputable section Normalization

/-- The normalization functor maps an object in `FreeSMC C` (a "tree") to its underlying (symmetric)
list of "leaves" in `SList C`.
It is defined using the universal property of the free symmetric monoidal category. -/
@[expose] public def normalization : FreeSymmetricMonoidalCategory C ‚•§ SList C :=
  FreeSymmetricMonoidalCategory.project (fun c => [c]~)

@[simp]
public lemma normalization_of (c : C) : (normalization C).obj (of c) = [c]~ := rfl

public instance : (normalization C).Monoidal :=
    inferInstanceAs (FreeSymmetricMonoidalCategory.project _).Monoidal
public instance : (normalization C).Braided :=
    inferInstanceAs (FreeSymmetricMonoidalCategory.project _).Braided

end Normalization

section Inclusion

open FreeSListQuiv

/-- The recursive data defining the inclusion functor `SList C ‚•§ FreeSMC C`. -/
def inclusionRecData : RecursiveFunctorData C (FreeSymmetricMonoidalCategory C) where
  nilObj := ùüô_ _
  consF c := tensorLeft (of c)
  swapIso x y := NatIso.ofComponents
    (fun Z => (Œ±_ _ _ _).symm ‚â™‚â´ (whiskerRightIso (Œ≤_ _ _) Z) ‚â™‚â´ (Œ±_ _ _ _))
    (fun f => by
      dsimp
      simp only [Category.assoc]
      simp_rw [ tensor_whiskerLeft_symm, Iso.hom_inv_id_assoc, ‚Üê whisker_exchange_assoc]
      simp)
  swap_inv c c' l := by simp [‚Üê comp_whiskerRight_assoc]
  hexagon c c' c'' l := by
    dsimp
    monoidal_simps
    simp_rw [pentagon_hom_inv_inv_inv_inv_assoc, pentagon_hom_hom_inv_hom_hom_assoc,
      whisker_assoc_symm_assoc, Iso.hom_inv_id_assoc, ‚Üê comp_whiskerRight_assoc]
    have :=
      ((Œ±_ _ _ _).hom ‚â´= BraidedCategory.yang_baxter (of c) (of c') (of c'')) =‚â´ (Œ±_ _ _ _).inv
    simp only [Iso.hom_inv_id_assoc, Category.assoc, Iso.hom_inv_id, Category.comp_id] at this
    simp [this]

/-- The inclusion functor `J : SList C ‚•§ FreeSMC C` mapping lists to right-associated trees. -/
public def J : SList C ‚•§ FreeSymmetricMonoidalCategory C :=
  (inclusionRecData C).functor

section
-- Computation rules for J

variable {C}

public lemma J_obj_unit : (J C).obj ([]~) = ùüô_ _ :=
  RecursiveFunctorData.functor_obj_nil _

public lemma J_obj_cons (c : C) (l : SList C) : (J C).obj (c ::~ l) = (of c) ‚äó (J C).obj l :=
  RecursiveFunctorData.functor_obj_cons _ _ _

@[expose] public def JObjNilIso : (J C).obj []~ ‚âÖ ùüô_ _ := eqToIso J_obj_unit

@[expose] public def JObjConsIso (c : C) (l : SList C) :
    (J C).obj (c ::~ l) ‚âÖ (of c) ‚äó (J C).obj l :=
  eqToIso <| J_obj_cons c l

lemma J_map_cons_map (c : C) {l l' : SList C} (f : l ‚ü∂ l') :
    (J C).map ((cons c).map f) =
      (JObjConsIso _ _).hom ‚â´
      ((of c) ‚óÅ (J C).map f) ‚â´
      (JObjConsIso _ _).inv :=
  RecursiveFunctorData.functor_map_cons_map _ _ _

lemma J_map_swap (c c' : C) (l : SList C) :
    (J C).map (Œ≤~ c c' l) =
    (JObjConsIso _ _).hom ‚â´
      ((of c) ‚óÅ (JObjConsIso _ _).hom) ‚â´
      ((Œ±_ _ _ _).symm ‚â™‚â´ (whiskerRightIso (Œ≤_ _ _) _) ‚â™‚â´ (Œ±_ _ _ _)).hom ‚â´
      ((of c') ‚óÅ (JObjConsIso _ _).inv) ‚â´
      (JObjConsIso _ _).inv :=
  RecursiveFunctorData.functor_map_swap _ _ _ _

end

end Inclusion

section MonoidalStructure

/-- The natural isomorphism of bifunctors J(-) ‚äó J(-) ‚ü∂ J(- ‚äó -)
that defines the monoidal structure on J. -/
public def ŒºIso :
    (Functor.whiskeringLeft‚ÇÇ _|>.obj (J C) |>.obj (J C) |>.obj
      (curriedTensor (FreeSymmetricMonoidalCategory C))) ‚âÖ
    (Functor.postcompose‚ÇÇ.obj (J C)).obj (curriedTensor (SList C)) :=
  recNatIso
    (NatIso.ofComponents
      (fun x ‚Ü¶ whiskerRightIso JObjNilIso _ ‚â™‚â´ (Œª_ _) ‚â™‚â´ (J C).mapIso ((Œª_ _).symm ‚â™‚â´
        whiskerRightIso unitIsoNil x))
      fun {x y} f ‚Ü¶ by
        dsimp
        monoidal_simps
        simp only [Functor.map_comp, whisker_exchange_assoc, id_whiskerLeft, Category.assoc,
          Iso.inv_hom_id_assoc]
        congr 2
        simp_rw [‚Üê Functor.map_comp]
        congr 1
        rw [hom_eq_iff_toEquiv_eq]
        ext i : 1
        cases i with
        | left i => simp only [length_nil] at i; exact Fin.elim0 i
        | right i => simp )
    (fun c l Œº ‚Ü¶
      NatIso.ofComponents
        (fun x ‚Ü¶
          whiskerRightIso (JObjConsIso _ _) _ ‚â™‚â´ (Œ±_ _ _ _) ‚â™‚â´ whiskerLeftIso (of c) (Œº.app _) ‚â™‚â´
            (JObjConsIso _ _).symm ‚â™‚â´ (J C).mapIso (tensorObjConsIso c l x).symm)
        fun {x y} f ‚Ü¶ by
          dsimp
          simp only [whisker_exchange_assoc, tensor_whiskerLeft, Category.assoc,
            Iso.inv_hom_id_assoc]
          congr 2
          have := NatIso.naturality_2 Œº f
          dsimp at this
          simp_rw [‚Üê MonoidalCategory.whiskerLeft_comp_assoc, ‚Üê this]
          simp [J_map_cons_map, whiskerLeft_cons])
    (fun u v l Œº ‚Ü¶ by
      ext x
      dsimp
      simp only [J_map_swap, Iso.trans_hom, Iso.symm_hom, whiskerRightIso_hom, Category.assoc,
        comp_whiskerRight, whisker_assoc, whiskerLeft_comp, inv_hom_whiskerRight_assoc,
        Iso.inv_hom_id_assoc]
      congr 3
      simp_rw [‚Üê whiskerLeft_comp_assoc, ‚Üê comp_whiskerRight_assoc, Iso.inv_hom_id]
      simp only [comp_whiskerRight, id_whiskerRight, Category.id_comp, whiskerLeft_comp,
        Category.assoc, pentagon_assoc]
      simp_rw [tensor_whiskerLeft_symm, Category.assoc, Iso.hom_inv_id_assoc,
        whiskerRight_tensor_symm_assoc, Iso.inv_hom_id_assoc, ‚Üê whisker_exchange_assoc]
      simp only [tensor_whiskerLeft, Category.assoc, pentagon_inv_hom_hom_hom_hom_assoc,
        Iso.inv_hom_id_assoc]
      congr 2
      simp only [whiskerRight_swap, Functor.map_comp, J_map_cons_map, J_map_swap, Iso.trans_hom,
        Iso.symm_hom, whiskerRightIso_hom, Category.assoc, Iso.inv_hom_id_assoc,
        Iso.map_inv_hom_id_assoc]
      simp_rw [‚Üê whiskerLeft_comp_assoc, Iso.map_inv_hom_id_assoc, Iso.inv_hom_id, whiskerLeft_id,
        Category.id_comp])
    (fun c l l' f Œº Œº' h ‚Ü¶ by
      ext x
      replace h := congr_app h x
      dsimp at Œº Œº' h ‚ä¢
      simp only [J_map_cons_map, comp_whiskerRight, whisker_assoc, Category.assoc,
        inv_hom_whiskerRight_assoc, Iso.inv_hom_id_assoc, whiskerRight_cons_map, Functor.map_comp,
        Iso.map_inv_hom_id_assoc]
      simp_rw [‚Üê whiskerLeft_comp_assoc, h])

@[local simp]
lemma ŒºIso_hom_app_nil_app (t : SList C) :
    ((ŒºIso C).hom.app []~).app t =
      JObjNilIso.hom ‚ñ∑ _ ‚â´ (Œª_ _).hom ‚â´ (J C).map ((Œª_ _).inv ‚â´
        unitIsoNil.hom ‚ñ∑ t) := by
  simp [ŒºIso]

@[local simp]
lemma ŒºIso_hom_app_cons_app (c : C) (l t : SList C) :
    ((ŒºIso C).hom.app (c ::~ l)).app t =
    (JObjConsIso _ _).hom ‚ñ∑ _ ‚â´ (Œ±_ _ _ _).hom ‚â´ (of c) ‚óÅ ((ŒºIso C).hom.app l).app t ‚â´
      (JObjConsIso _ _).inv ‚â´ (J C).map (tensorObjConsIso c l _).inv := by
  simp [ŒºIso]

@[reassoc (attr := local simp)]
lemma ŒºIso_natural_left {x y : SList C}
    (f : x ‚ü∂ y) (z : SList C) :
    (J C).map f ‚ñ∑ (J C).obj z ‚â´ ((ŒºIso C).hom.app y).app z =
    ((ŒºIso C).hom.app x).app z ‚â´ (J C).map (f ‚ñ∑ z) := by
  simpa [-NatTrans.naturality] using congr_app ((ŒºIso C).hom.naturality f) z

@[reassoc (attr := local simp)]
lemma ŒºIso_natural_right
    (z : SList C)
    {x y : SList C} (f : x ‚ü∂ y) :
    (J C).obj z ‚óÅ (J C).map f ‚â´ ((ŒºIso C).hom.app z).app y =
    ((ŒºIso C).hom.app z).app x ‚â´ (J C).map (z ‚óÅ f) := by
  simpa [-NatTrans.naturality] using ((ŒºIso C).hom.app z).naturality f

/-- Auxiliary definition to define the monoidal structure on J C. -/
@[expose] public def JCoreMonoidal : (J C).CoreMonoidal where
  ŒµIso := JObjNilIso.symm ‚â™‚â´ (J C).mapIso unitIsoNil.symm
  ŒºIso x y := ((ŒºIso C).app x).app y
  ŒºIso_hom_natural_left {x y} f z := by simp
  ŒºIso_hom_natural_right x {y z} f := by simp
  associativity x y z := by
    induction x using SList.cons_induction with
    | nil =>
      dsimp
      simp only [ŒºIso_hom_app_nil_app, Functor.map_comp, comp_whiskerRight,
        leftUnitor_whiskerRight, Category.assoc, ŒºIso_natural_left_assoc,
        Functor.postcompose‚ÇÇ_obj_obj_obj_obj, curriedTensor_obj_obj, leftUnitor_inv_whiskerRight,
        whiskerRight_tensor]
      simp_rw [whisker_exchange_assoc, ‚Üê Functor.map_comp_assoc]
      simp
    | cons c x hr =>
      simp only [Iso.app_hom, ŒºIso_hom_app_cons_app, Functor.postcompose‚ÇÇ_obj_obj_obj_obj,
        curriedTensor_obj_obj, comp_whiskerRight, whisker_assoc, Category.assoc,
        ŒºIso_natural_left_assoc, inv_hom_whiskerRight_assoc, Iso.inv_hom_id_assoc] at hr ‚ä¢
      simp_rw [whiskerRight_tensor_symm_assoc, whisker_exchange_assoc]
      congr 2
      simp only [pentagon_inv_hom_hom_hom_inv_assoc, tensor_whiskerLeft, Category.assoc,
        Iso.inv_hom_id_assoc, Iso.cancel_iso_hom_left]
      have := congr(of c ‚óÅ $hr) =‚â´ of c ‚óÅ (J C).map (Œ±_ x y z).inv
      simp_rw [‚Üê whiskerLeft_comp, Category.assoc, Iso.map_hom_inv_id] at this
      simp_rw [Category.comp_id, whiskerLeft_comp] at this
      simp_rw [reassoc_of% this, whiskerLeft_inv_hom_assoc]
      congr 2
      -- Now all remainaing morphisms are eqToHoms under the hood, so we don‚Äôt have to try
      -- to be too smart.
      -- An alternative proof that does not use strictness is to show that JObjConsIso is
      -- natural in the second var.
      rw [show (Œ±_ x y z) = SList.associator x y z by rfl,
        show Œ±_ (c ::~ x) y z = SList.associator _ _ _ by rfl]
      simp [SList.associator, tensorObjConsIso, JObjConsIso, eqToHom_map]
  left_unitality x := by simp [‚Üê Functor.map_comp_assoc]
  right_unitality x := by
    induction x using SList.cons_induction with
    | nil => simp [whisker_exchange_assoc, unitors_equal, unitors_inv_equal]
    | cons c x hr =>
      simp only [Functor.mapIso_symm, Iso.trans_hom, Iso.symm_hom, Functor.mapIso_inv,
        whiskerLeft_comp, Iso.app_hom, Category.assoc, ŒºIso_natural_right_assoc,
        Functor.postcompose‚ÇÇ_obj_obj_obj_obj, curriedTensor_obj_obj,
        ŒºIso_hom_app_cons_app] at hr ‚ä¢
      -- here, we basically want to change the ((ŒºIso C).hom.app x).app (ùüô_ (SList C)) term
      -- to a ((ŒºIso C).hom.app x).app ([]~) that appears in hr, to simplify things.
      have := ŒºIso_natural_right C x unitIsoNil.hom =‚â´ (J C).map (x ‚óÅ unitIsoNil.inv)
      simp only [Functor.postcompose‚ÇÇ_obj_obj_obj_obj, curriedTensor_obj_obj, Category.assoc,
        Functor.whiskeringLeft‚ÇÇ_obj_obj_obj_obj_obj, ‚Üê Functor.map_comp, whiskerLeft_hom_inv,
        Functor.map_id, Category.comp_id] at this
      simp_rw [‚Üê this, whiskerLeft_comp_assoc]
      have := (J C).obj x ‚óÅ JObjNilIso.hom ‚â´= hr =‚â´
        (inv <| (J C).map (x ‚óÅ unitIsoNil.inv) ‚â´ (J C).map (œÅ_ x).hom)
      simp only [IsIso.inv_comp, Category.assoc, IsIso.hom_inv_id_assoc, IsIso.hom_inv_id,
        Category.comp_id, whiskerLeft_hom_inv_assoc] at this
      simp only [‚Üê this, whiskerLeft_comp, whiskerLeft_rightUnitor, Category.assoc,
        whiskerLeft_inv_hom'_assoc]
      simp_rw [tensor_whiskerLeft_symm_assoc, Iso.hom_inv_id_assoc, whisker_exchange_assoc,
        ‚Üê whiskerLeft_comp_assoc, Iso.map_inv_hom_id_assoc]
      simp only [whiskerRight_id, Iso.inv_hom_id, whiskerLeft_id, Category.id_comp, Category.assoc,
        Iso.inv_hom_id_assoc, ‚Üê cancel_epi (œÅ_ ((J C).obj (c ::~ x))).inv]
      -- Now everything is strict.
      simp [show (œÅ_ _) = SList.rightUnitor _ by rfl,
        SList.rightUnitor, tensorObjConsIso, JObjConsIso, eqToHom_map]

@[expose] public instance : (J C).Monoidal := JCoreMonoidal C|>.toMonoidal

variable {C}

@[local simp]
lemma J_Œº_app_nil_app (t : SList C) :
    Functor.LaxMonoidal.Œº (J C) []~ t =
      JObjNilIso.hom ‚ñ∑ _ ‚â´ (Œª_ _).hom ‚â´ (J C).map ((Œª_ _).inv ‚â´
        unitIsoNil.hom ‚ñ∑ t) :=
  ŒºIso_hom_app_nil_app C _

@[local simp]
lemma J_Œº_app_cons_app (c : C) (l t : SList C) :
    Functor.LaxMonoidal.Œº (J C) (c ::~ l) t =
    (JObjConsIso _ _).hom ‚ñ∑ _ ‚â´ (Œ±_ _ _ _).hom ‚â´ (of c) ‚óÅ Functor.LaxMonoidal.Œº (J C) l t ‚â´
      (JObjConsIso _ _).inv ‚â´ (J C).map (tensorObjConsIso c l _).inv :=
  ŒºIso_hom_app_cons_app C _ _ _

lemma J_Œµ :
    Functor.LaxMonoidal.Œµ (J C) = JObjNilIso.inv ‚â´ (J C).map unitIsoNil.inv :=
  rfl

lemma J_Œ∑ :
    Functor.OplaxMonoidal.Œ∑ (J C) = (J C).map unitIsoNil.hom ‚â´ JObjNilIso.hom :=
  rfl

/-- A version of `braiding_inv_tensorUnit_right` seen through JObjNilIso. -/
lemma braiding_inv_J_obj_nil_right (x : FreeSymmetricMonoidalCategory C) :
    (Œ≤_ x ((J C).obj []~)).inv =
    JObjNilIso.hom ‚ñ∑ x ‚â´ (Œª_ x).hom ‚â´ (œÅ_ x).inv ‚â´ x ‚óÅ JObjNilIso.inv := by
  have := BraidedCategory.braiding_naturality_right x JObjNilIso.hom =‚â´ JObjNilIso.inv ‚ñ∑ x
  simp at this
  rw [‚Üê IsIso.inv_eq_inv]
  simp [‚Üê this]

/-- A version of `braiding_inv_tensorUnit_right` seen through JObjNilIso. -/
lemma braiding_hom_J_obj_nil_right (x : FreeSymmetricMonoidalCategory C) :
    (Œ≤_ x ((J C).obj []~)).hom =
     x ‚óÅ JObjNilIso.hom ‚â´ (œÅ_ x).hom ‚â´ (Œª_ x).inv ‚â´ JObjNilIso.inv ‚ñ∑ x := by
  have := BraidedCategory.braiding_naturality_right x JObjNilIso.hom =‚â´ JObjNilIso.inv ‚ñ∑ x
  simp at this
  rw [‚Üê IsIso.inv_eq_inv]
  simp [‚Üê this]

lemma J_braided_nil_nil :
    Functor.LaxMonoidal.Œº (J C) []~ []~ ‚â´ (J C).map (Œ≤_ []~ []~).hom =
    (Œ≤_ ((J C).obj []~) ((J C).obj []~)).hom ‚â´ Functor.LaxMonoidal.Œº (J C) []~ []~ := by
  simp only [Functor.CoreMonoidal.toMonoidal_toLaxMonoidal, J_Œº_app_nil_app,
    Functor.LaxMonoidal.left_unitality, Functor.map_comp, Category.assoc,
    Iso.map_hom_inv_id_assoc, braiding_hom_app_nil]
  simp_rw [‚Üê Functor.LaxMonoidal.Œº_natural_left, ‚Üê Functor.LaxMonoidal.Œº_natural_left_assoc,
    J_Œº_app_nil_app, J_Œµ, Category.assoc, Functor.map_comp,
    ‚Üê BraidedCategory.braiding_naturality_right_assoc, braiding_tensorUnit_right]
  simp_rw [‚Üê whiskerLeft_comp_assoc, ‚Üê comp_whiskerRight_assoc,
    ‚Üê Functor.map_comp_assoc, ‚Üê Functor.map_comp, Category.assoc,
    Iso.inv_hom_id_assoc, Iso.hom_inv_id_assoc, Iso.map_inv_hom_id_assoc,
    ‚Üê whisker_exchange_assoc]
  simp [-Functor.LaxMonoidal.left_unitality, -Functor.LaxMonoidal.right_unitality,
    ‚Üê unitors_equal, ‚Üê cancel_epi ((J C).obj []~ ‚óÅ JObjNilIso.inv), whisker_exchange_assoc,
    ‚Üê unitors_inv_equal]

lemma J_braided_nil_left (x : SList C) :
    Functor.LaxMonoidal.Œº (J C) x []~ ‚â´ (J C).map (Œ≤_ x []~).hom =
    (Œ≤_ ((J C).obj x) ((J C).obj []~)).hom ‚â´ Functor.LaxMonoidal.Œº (J C) []~ x := by
  induction x using SList.cons_induction with
  | nil => exact J_braided_nil_nil
  | cons c x hr =>
    simp only [Functor.CoreMonoidal.toMonoidal_toLaxMonoidal, J_Œº_app_cons_app]
    have := hr =‚â´ (J C).map (Œ≤_ x []~).inv
    simp only [Category.assoc, Iso.map_hom_inv_id, Category.comp_id] at this
    rw [this]
    simp only [Category.assoc, J_Œº_app_nil_app]
    rw [‚Üê cancel_epi ((JObjConsIso c x).inv ‚ñ∑ (J C).obj []~)]
    simp only [inv_hom_whiskerRight_assoc, BraidedCategory.braiding_naturality_left_assoc,
      whisker_exchange_assoc, whiskerRight_tensor_assoc, leftUnitor_naturality_assoc]
    rw [‚Üê cancel_epi (Œ±_ (of c) ((J C).obj x) ((J C).obj []~)).inv]
    simp only [Iso.inv_hom_id_assoc, BraidedCategory.hexagon_reverse_assoc,
      SList.braiding_hom_app_nil, whiskerLeft_comp, Category.assoc,
      SList.braiding_inv_app_nil,
      SList.whiskerLeft_cons, Category.assoc, cancel_epi]
    -- At this point all of the brainding have been rewritten,
    -- and what remains is "pure" monoidal category stuff
    simp only [Functor.map_comp, whiskerLeft_comp, Iso.map_inv_hom_id_assoc, Category.assoc]
    simp only [‚Üê Category.assoc, cancel_mono]
    simp only [Category.assoc, braiding_hom_J_obj_nil_right]
    simp only [comp_whiskerRight, whisker_assoc, leftUnitor_inv_whiskerRight, Category.assoc,
      triangle_assoc_comp_right_assoc, Iso.inv_hom_id_assoc, cancel_epi]
    simp_rw [‚Üê whiskerLeft_comp_assoc, ‚Üê Functor.map_comp_assoc, hom_inv_whiskerRight_assoc,
      Iso.inv_hom_id_assoc, ‚Üê comp_whiskerRight_assoc]
    simp only [‚Üê Functor.map_comp, Category.assoc, inv_hom_whiskerRight, id_whiskerRight,
      Category.id_comp, Iso.inv_hom_id_assoc, ]
    simp only [Functor.map_comp, J_map_cons_map, Category.assoc, Iso.inv_hom_id_assoc,
      ‚Üê whiskerLeft_comp_assoc]
    have := (JObjConsIso c x).inv ‚â´= J_map_cons_map c ((œÅ_ x).inv)
    simp only [Iso.inv_hom_id_assoc] at this
    simp only [‚Üê Functor.map_comp, whiskerLeft_hom_inv, Category.comp_id,
      ‚Üê reassoc_of% this, ‚Üê cancel_epi (JObjConsIso c x).hom, Iso.hom_inv_id_assoc, Iso.hom_inv_id,
      ‚Üê Functor.map_id]
    congr 1
    -- and now everything lives in SList and are morphisms known to be eqToHoms.
    simp [show (œÅ_ _) = SList.rightUnitor _ by rfl,
      SList.rightUnitor, tensorObjConsIso, eqToHom_map]

lemma J_braided_nil_right (x : SList C) :
    Functor.LaxMonoidal.Œº (J C) []~ x ‚â´ (J C).map (Œ≤_ []~ x).hom =
    (Œ≤_ ((J C).obj []~) ((J C).obj x)).hom ‚â´ Functor.LaxMonoidal.Œº (J C) x []~ := by
  simp_rw [SymmetricCategory.braiding_swap_eq_inv_braiding]
  have := J_braided_nil_left x =‚â´  (J C).map (Œ≤_ x []~).inv
  simp only [Category.assoc, Iso.map_hom_inv_id, Category.comp_id] at this
  rw [this]
  simp

@[reassoc]
private lemma whiskerLeft_map_hom_inv
    {C D : Type*} [Category* C] [Category* D] [MonoidalCategory D]
    (x : D) {y z : C} (F : C ‚•§ D) (e : y ‚âÖ z) :
    x ‚óÅ F.map e.hom ‚â´ x ‚óÅ F.map e.inv = ùüô _ := by
  simp [‚Üê whiskerLeft_comp]

@[reassoc]
private lemma whiskerLeft_map_inv_hom
    {C D : Type*} [Category* C] [Category* D] [MonoidalCategory D]
    (x : D) {y z : C} (F : C ‚•§ D) (e : y ‚âÖ z) :
    x ‚óÅ F.map e.inv ‚â´ x ‚óÅ F.map e.hom = ùüô _ := by
  simp [‚Üê whiskerLeft_comp]

@[reassoc]
private lemma whiskerLeft_whiskerLeft_inv_hom
    {D : Type*} [Category* D] [MonoidalCategory D]
    (x y : D) {z t : D} (e : z ‚âÖ t) :
    x ‚óÅ y ‚óÅ e.inv ‚â´ x ‚óÅ y ‚óÅ e.hom = ùüô _ := by
  simp [‚Üê whiskerLeft_comp]

@[reassoc]
private lemma whiskerLeft_whiskerLeft_hom_inv
    {D : Type*} [Category* D] [MonoidalCategory D]
    (x y : D) {z t : D} (e : z ‚âÖ t) :
    x ‚óÅ y ‚óÅ e.hom ‚â´ x ‚óÅ y ‚óÅ e.inv = ùüô _ := by
  simp [‚Üê whiskerLeft_comp]

@[reassoc]
private lemma whiskerLeft_hom_inv_whiskerRight
    {D : Type*} [Category* D] [MonoidalCategory D]
    (x : D) {y z : D} (e : y ‚âÖ z) (t : D) :
  x ‚óÅ e.hom ‚ñ∑ t ‚â´ x ‚óÅ e.inv ‚ñ∑ t = ùüô _ := by
  simp [‚Üê whiskerLeft_comp]

@[reassoc]
private lemma whiskerLeft_inv_hom_whiskerRight
    {D : Type*} [Category* D] [MonoidalCategory D]
    (x : D) {y z : D} (e : y ‚âÖ z) (t : D) :
  x ‚óÅ e.inv ‚ñ∑ t ‚â´ x ‚óÅ e.hom ‚ñ∑ t = ùüô _ := by
  simp [‚Üê whiskerLeft_comp]

/-- The functor `J C` is braided (and hence symmetric). Note that by the universal
property of the free symmetric monoidal category, this statement "defines"
the counit of the equivalence `SList C ‚âå FreeSymmetricMonoidalCategory C`, as thanks to
it both `ùü≠ _` and `K C ‚ãô J C` are symmetric monoidal functors with
isomorphic values on the generators. -/
public instance : (J C).Braided where
  braided x y := by
    induction y using SList.cons_induction generalizing x with
    | nil => exact J_braided_nil_left _
    | cons c l ih =>
      induction x using SList.cons_induction generalizing c l with
      | nil => exact J_braided_nil_right _
      | cons c' l' hr =>
        /- Clearly the hardest computation here. This one is a massive pain.
        First the idea is to get rid of the terms of form
        Functor.LaxMonoidal.Œº (J C) (c ::~ l) (c' ::~ l') and use existing commutations
        to partially swap out some terms. -/
        simp only [Functor.CoreMonoidal.toMonoidal_toLaxMonoidal, J_Œº_app_cons_app]
        have := hr c l ih =‚â´ (J C).map (Œ≤_ l' (c ::~ l)).inv
        simp only [Category.assoc, Iso.map_hom_inv_id, Category.comp_id] at this
        simp only [this, J_Œº_app_cons_app]
        have := (Œ≤_ ((J C).obj (c' ::~ l')) ((J C).obj l)).inv ‚â´= ih (c' ::~ l')
        simp only [Iso.inv_hom_id_assoc] at this
        simp only [‚Üê this, J_Œº_app_cons_app, Category.assoc,
          ‚Üê SymmetricCategory.braiding_swap_eq_inv_braiding]
        /- Then we want to get rid of the `(J C).obj (c ::~ l)` in arguments of the
        braiding, turning them into `of c ‚äó (J C).obj l`. -/
        simp_rw [‚Üê cancel_epi ((JObjConsIso c' l').inv ‚ñ∑ (J C).obj (c ::~ l)),
          inv_hom_whiskerRight_assoc, BraidedCategory.braiding_naturality_left_assoc,
          SymmetricCategory.braiding_swap_eq_inv_braiding (((J C).obj (c' ::~ l'))),
          SymmetricCategory.braiding_swap_eq_inv_braiding (((J C).obj (c ::~ l))),
          ‚Üê BraidedCategory.braiding_inv_naturality_right_assoc, whisker_exchange_assoc]
        simp_rw [‚Üê BraidedCategory.braiding_inv_naturality_right_assoc]
        /- Then, we bring up the computation rules for the braiding in SList, which describes
        `Œ≤_ (c ::~ l) _` -/
        simp_rw [SList.braiding_hom_cons_right, SList.braiding_hom_cons_left]
        /- then a short round of "cleaning up" -/
        simp only [whiskerLeft_comp, Category.assoc, Functor.map_comp,
          J_map_cons_map, Iso.inv_hom_id_assoc, whiskerLeft_map_inv_hom_assoc,
          whiskerLeft_inv_hom_assoc]
        /- cancel redundant terms on the right -/
        simp only [‚Üê Category.assoc, cancel_mono]
        simp only [Category.assoc]
        simp only [associator_naturality_right_assoc, ‚Üê Iso.eq_inv_comp,
          ‚Üê associator_inv_naturality_right_assoc, whiskerLeft_whiskerLeft_inv_hom_assoc,
          cancel_epi]
        /- Now we want to get rid of the tensors in arguments of braidings. -/
        simp only [BraidedCategory.braiding_tensor_left_inv, Category.assoc,
          BraidedCategory.braiding_tensor_right_inv]
        /- A second cleanup round is able to clear some noise now. -/
        monoidal_simps
        simp only [Functor.CoreMonoidal.toMonoidal_toLaxMonoidal, Functor.comp_obj,
          Functor.flip_obj_obj, curriedTensor_obj_obj, whiskerLeft_inv_hom_assoc,
          pentagon_hom_hom_inv_hom_hom_assoc, inv_hom_whiskerRight_assoc, Iso.inv_hom_id_assoc,
          pentagon_inv_assoc, cancel_epi]
        /- Now we rewrite Functor.LaxMonoidal.Œº (J C) l' l in terms of
        Functor.LaxMonoidal.Œº (J C) l l' -/
        have := ih l' =‚â´ (J C).map (Œ≤_ l' l).inv
        simp only [Functor.CoreMonoidal.toMonoidal_toLaxMonoidal, Category.assoc,
          Iso.map_hom_inv_id, Category.comp_id] at this
        simp only [this, whiskerLeft_comp, whiskerLeft_whiskerLeft_inv_hom_assoc,
          ‚Üê SymmetricCategory.braiding_swap_eq_inv_braiding (of c'),
          whiskerLeft_inv_hom_whiskerRight_assoc]
        /- Now we have to perform more clean up, using the computation rules for the braiding in
        SList. -/
        simp only [Q_inv_app_cons, Functor.comp_obj, Functor.flip_obj_obj, curriedTensor_obj_obj,
          Functor.map_comp, J_map_cons_map, J_map_swap, Iso.trans_hom, Iso.symm_hom,
          whiskerRightIso_hom, Category.assoc, Iso.inv_hom_id_assoc, Iso.map_inv_hom_id_assoc,
          pentagon_assoc] -- generated by simp? [Q_inv_app_cons, J_map_cons_map, J_map_swap]
        simp only [‚Üê whiskerLeft_comp_assoc, ‚Üê Functor.map_comp_assoc,
          Iso.hom_inv_id_app_assoc, Iso.inv_hom_id]
        simp only [whiskerLeft_comp, Functor.map_id, Category.id_comp, Iso.inv_hom_id,
          Category.comp_id, Iso.map_inv_hom_id, whiskerLeft_inv_hom, Category.assoc]
        simp only [‚Üê SymmetricCategory.braiding_swap_eq_inv_braiding (of c)]
        simp only [tensor_whiskerLeft_symm_assoc, Iso.hom_inv_id_assoc, cancel_epi,
          whisker_exchange_assoc, whiskerRight_tensor_symm_assoc, Iso.inv_hom_id_assoc]
        simp [SymmetricCategory.braiding_swap_eq_inv_braiding]

end MonoidalStructure

noncomputable section Equivalence

attribute [-simp] Functor.LaxMonoidal.left_unitality

/-- The natural isomorphism `Œ∑ : ùü≠ ‚âÖ J ‚ãô K` on `SList C`. -/
public def unitIso : ùü≠ (SList C) ‚âÖ J C ‚ãô normalization C :=
  recNatIso
    (SList.unitIsoNil.symm ‚â™‚â´ (Functor.Monoidal.ŒµIso _) ‚â™‚â´
      (normalization C).mapIso JObjNilIso.symm) -- nil
    (fun x p e‚ÇÄ ‚Ü¶
      (x>~).mapIso ((Œª_ p).symm ‚â™‚â´
        whiskerRightIso SList.unitIsoNil p) ‚â™‚â´
        (SList.tensorObjConsIso x []~ p).symm ‚â™‚â´
        whiskerLeftIso _ e‚ÇÄ ‚â™‚â´
        (Functor.Monoidal.ŒºIso (normalization C) (of x) ((J C).obj p)) ‚â™‚â´
        (normalization C).mapIso (JObjConsIso x p).symm) --cons
    (fun x y l' p ‚Ü¶ by
      -- simp? [J_map_swap]
      simp only [Functor.id_obj, Functor.comp_obj, Functor.id_map, Functor.mapIso_trans,
        Functor.mapIso_symm, Iso.trans_assoc, whiskerLeftIso_trans, Iso.trans_hom, Iso.symm_hom,
        Functor.mapIso_inv, Functor.mapIso_hom, whiskerRightIso_hom, whiskerLeftIso_hom,
        Functor.Monoidal.ŒºIso_hom, Functor.CoreMonoidal.toMonoidal_toLaxMonoidal, Functor.comp_map,
        J_map_swap, Category.assoc, Functor.map_comp, Iso.map_inv_hom_id_assoc]
      have e‚ÇÅ := Functor.LaxMonoidal.Œº_natural_right (normalization C) (of x) (JObjConsIso y l').inv
      have e‚ÇÇ := Functor.LaxMonoidal.Œº_natural_right (normalization C) (of y) (JObjConsIso x l').inv
      dsimp at e‚ÇÅ e‚ÇÇ
      simp only [reassoc_of% e‚ÇÅ, ‚Üê Functor.map_comp, whiskerLeft_inv_hom_assoc, reassoc_of% e‚ÇÇ]
      simp only [Functor.map_comp, ‚Üê Category.assoc, cancel_mono]; simp only [Category.assoc]
      simp_rw [Functor.Monoidal.map_whiskerRight_assoc (F := normalization C), Functor.map_braiding,
        Functor.Monoidal.map_associator_inv]
      -- simp?
      simp only [normalization_of, Functor.CoreMonoidal.toMonoidal_toOplaxMonoidal,
        Functor.CoreMonoidal.toMonoidal_toLaxMonoidal, comp_whiskerRight, Category.assoc,
        Functor.LaxMonoidal.associativity, Functor.Monoidal.Œº_Œ¥_assoc,
        Functor.Monoidal.whiskerRight_Œº_Œ¥_assoc, Functor.Monoidal.whiskerLeft_Œº_Œ¥_assoc]
      --cancel equal terms on the right, we definitely need a simproc for this
      simp only [‚Üê Category.assoc, cancel_mono]; simp only [Category.assoc]
      simp_rw [tensor_whiskerLeft_symm_assoc, Iso.hom_inv_id_assoc, whisker_exchange_assoc]
      simp only [tensor_whiskerLeft, Category.assoc, Iso.inv_hom_id, Category.comp_id]
      simp only [‚Üê Category.assoc, cancel_mono]; simp only [Category.assoc]
      -- at this point everything remaining is "noise" in SList around Œ≤~ x y l' or Œ≤_ [x]~ [y]~ l'.
      -- So we can just check that the remaining morphisms give the same permutations
      rw [hom_eq_iff_toEquiv_eq]
      ext i
      cases i using finTensorObjCases with
      | left i =>
        cases i using fin_cons_obj_cases with
        | right i => simp only [length_nil] at i; exact Fin.elim0 i
        | zero =>
          simp [toEquiv_tensorObjConsIso_inv_I_right, toEquiv_tensorObjConsIso_inv_I‚ÇÄ,
            toEquiv_cons_map_I‚ÇÄ, toEquiv_cons_map_I_right, toEquiv_swap_I‚ÇÄ]
      | right i =>
        cases i using finTensorObjCases with
        | left i =>
          cases i using fin_cons_obj_cases with
          | right i => simp only [length_nil] at i; exact Fin.elim0 i
          | zero =>
            simp [toEquiv_tensorObjConsIso_inv_I_right, toEquiv_tensorObjConsIso_inv_I‚ÇÄ,
              toEquiv_cons_map_I‚ÇÄ, toEquiv_cons_map_I_right, toEquiv_swap_I_I‚ÇÄ_natAdd]
        | right i =>
          simp [toEquiv_tensorObjConsIso_inv_I_right, toEquiv_cons_map_I_right, toEquiv_swap_I_I])
    (fun x l l' f pl pl' h ‚Ü¶ by
      dsimp at h ‚ä¢
      have := congr(x::~‚Çò $h)
      simp only [Functor.map_comp] at this
      simp only [Functor.map_comp, whiskerLeft_cons, Category.assoc, Iso.inv_hom_id_assoc,
        J_map_cons_map, Iso.map_inv_hom_id_assoc]
      simp_rw [‚Üê Functor.map_comp_assoc, ‚Üê Functor.map_comp, ‚Üê whisker_exchange,
        leftUnitor_inv_naturality_assoc, ‚Üê whiskerLeft_comp_assoc, h]
      simp only [whiskerLeft_comp, id_whiskerLeft, Category.assoc, Iso.inv_hom_id_assoc,
        Functor.map_comp, cancel_epi]
      simp only [‚Üê Category.assoc, cancel_mono]; simp only [Category.assoc]
      simp_rw [Functor.Monoidal.map_whiskerLeft, Functor.Monoidal.Œº_Œ¥_assoc]
      simp only [Functor.OplaxMonoidal.left_unitality,
        Functor.CoreMonoidal.toMonoidal_toOplaxMonoidal, Functor.map_comp, Category.assoc,
        normalization_of, whiskerLeft_cons, Functor.CoreMonoidal.toMonoidal_toLaxMonoidal,
        Iso.inv_hom_id_assoc]
      simp only [‚Üê Category.assoc, cancel_mono]; simp only [Category.assoc]
      simp only [‚Üê Functor.map_comp, ‚Üê Functor.map_comp_assoc]; congr 1
      simp [‚Üê whisker_exchange, -Functor.map_comp, ‚Üê Functor.map_comp_assoc ])

@[simp, reassoc]
lemma unitIso_hom_app_nil :
    (unitIso C).hom.app []~ =
    unitIsoNil.inv ‚â´
      Functor.LaxMonoidal.Œµ (normalization C) ‚â´
      (normalization C).map JObjNilIso.inv := by
  simp [unitIso]

@[simp, reassoc]
lemma unitIso_hom_app_cons (c : C) (l : SList C) :
    (unitIso C).hom.app (c ::~ l) =
    (c ::~‚Çò (Œª_ l).inv) ‚â´
    (c ::~‚Çò unitIsoNil.hom ‚ñ∑ l) ‚â´
      (tensorObjConsIso c []~ l).inv ‚â´
      [c]~ ‚óÅ (unitIso C).hom.app l ‚â´
      Functor.LaxMonoidal.Œº (normalization C) (of c) ((J C).obj l) ‚â´
      (normalization C).map (JObjConsIso c l).inv := by
  simp [unitIso]

@[simp, reassoc]
lemma unitIso_inv_app_nil :
    (unitIso C).inv.app []~ =
      (normalization C).map JObjNilIso.hom ‚â´
       Functor.OplaxMonoidal.Œ∑ (normalization C) ‚â´ unitIsoNil.hom := by
  rw [‚Üê IsIso.inv_eq_inv]
  simp [unitIso]

@[simp, reassoc]
lemma unitIso_inv_app_cons (c : C) (l : SList C) :
    (unitIso C).inv.app (c ::~ l) =
    (normalization C).map (JObjConsIso c l).hom ‚â´
      Functor.OplaxMonoidal.Œ¥ (normalization C) (of c) ((J C).obj l) ‚â´
      [c]~ ‚óÅ (unitIso C).inv.app l ‚â´
      (tensorObjConsIso c []~ l).hom ‚â´
      (c ::~‚Çò unitIsoNil.inv ‚ñ∑ l) ‚â´
      (c ::~‚Çò (Œª_ l).hom) := by
  rw [‚Üê IsIso.inv_eq_inv]
  simp only [‚Üê Functor.map_inv, IsIso.Iso.inv_hom, IsIso.inv_comp]
  simp [unitIso]

@[reassoc]
lemma unitIso_inv_app_singleton (c : C) :
    (unitIso C).inv.app ([c]~) =
    (normalization C).map ((JObjConsIso c []~).hom ‚â´ of c ‚óÅ JObjNilIso.hom ‚â´ (œÅ_ (of c)).hom) := by
  simp only [Functor.comp_obj, Functor.id_obj, unitIso_inv_app_cons, normalization_of,
    Functor.CoreMonoidal.toMonoidal_toOplaxMonoidal, unitIso_inv_app_nil, whiskerLeft_comp,
    Category.assoc, Functor.map_comp, Functor.Monoidal.map_whiskerLeft,
    Functor.CoreMonoidal.toMonoidal_toLaxMonoidal, Functor.Monoidal.map_rightUnitor,
    Functor.Monoidal.Œº_Œ¥_assoc]
  simp only [whiskerLeft_cons, whiskerLeft_nil, Functor.OplaxMonoidal.left_unitality,
    unitors_inv_equal, Functor.CoreMonoidal.toMonoidal_toOplaxMonoidal, Category.assoc,
    ‚Üê Functor.map_comp_assoc, whiskerRight_id, Iso.inv_hom_id_assoc, ‚Üê Functor.map_comp,
    hom_inv_whiskerRight_assoc, Iso.inv_hom_id, Category.comp_id, Iso.hom_inv_id_assoc,
    Functor.OplaxMonoidal.left_unitality_hom_assoc]
  congr 3
  simp only [‚Üê unitors_inv_equal]
  simp [rightUnitor_cons]

@[reassoc]
lemma unitIso_hom_app_singleton (c : C) :
    (unitIso C).hom.app ([c]~) =
    (normalization C).map ((œÅ_ (of c)).inv ‚â´ of c ‚óÅ JObjNilIso.inv ‚â´ (JObjConsIso c []~).inv) := by
  rw [‚Üê IsIso.inv_eq_inv, ‚Üê NatIso.isIso_inv_app, IsIso.Iso.inv_hom, unitIso_inv_app_singleton]
  simp [‚Üê Functor.map_inv]

public instance : (unitIso C).hom.IsMonoidal where
  unit := by
    have := (unitIso C).hom.naturality unitIsoNil.inv
    simp only [Functor.id_obj, Functor.comp_obj, Functor.id_map, unitIso_hom_app_nil,
      Functor.comp_map, Category.assoc, Iso.cancel_iso_inv_left] at this
    simp [this, J_Œµ]
  tensor x y := by
    induction x using SList.cons_induction with
    | nil =>
      let e‚ÇÅ : []~ ‚äó y ‚âÖ y := whiskerRightIso unitIsoNil.symm y ‚â™‚â´ Œª_ _
      have nat‚ÇÅ := (unitIso C).hom.naturality e‚ÇÅ.inv
      simp only [Functor.id_obj, Functor.comp_obj, Functor.id_map, Functor.comp_map] at nat‚ÇÅ
      simp only [Functor.id_obj, Functor.comp_obj, Functor.LaxMonoidal.id_Œº, Category.id_comp,
        unitIso_hom_app_nil, Functor.LaxMonoidal.comp_Œº]
      rw [‚Üê cancel_epi e‚ÇÅ.inv, nat‚ÇÅ]
      -- simp? [tensorHom_def, e‚ÇÅ, ‚Üê whisker_exchange_assoc, cancel_epi, J_Œº_app_nil_app] says
      simp only [Functor.comp_obj, Iso.trans_inv, whiskerRightIso_inv, Iso.symm_inv,
        Functor.map_comp, tensorHom_def, comp_whiskerRight, Category.assoc,
        ‚Üê whisker_exchange_assoc, Functor.LaxMonoidal.Œº_natural_left_assoc, id_whiskerLeft,
        Functor.OplaxMonoidal.left_unitality, Functor.CoreMonoidal.toMonoidal_toOplaxMonoidal,
        Functor.Monoidal.whiskerRight_Œ∑_Œµ_assoc, Functor.Monoidal.Œ¥_Œº_assoc,
        hom_inv_whiskerRight_assoc, Iso.inv_hom_id_assoc, cancel_epi, e‚ÇÅ]
      simp [‚Üê Functor.map_comp, J_Œº_app_nil_app]
    | cons c x hr =>
      simp only [Functor.id_obj, Functor.comp_obj, Functor.LaxMonoidal.id_Œº, Category.id_comp,
        Functor.LaxMonoidal.comp_Œº, unitIso_hom_app_cons] at hr ‚ä¢
      have nat‚ÇÅ := (unitIso C).hom.naturality (tensorObjConsIso c x y).inv
      simp only [Functor.id_obj, Functor.comp_obj, Functor.id_map, unitIso_hom_app_cons,
        whiskerRight_tensor, Functor.map_comp, Category.assoc, Functor.comp_map] at nat‚ÇÅ
      rw [‚Üê cancel_epi (tensorObjConsIso c x y).inv, nat‚ÇÅ, hr]
      simp only [Functor.comp_obj, tensorHom_def, Category.assoc, whiskerLeft_comp,
        comp_whiskerRight, whisker_assoc, J_Œº_app_cons_app,
        Functor.CoreMonoidal.toMonoidal_toLaxMonoidal, Functor.map_comp]
      simp_rw [Functor.Monoidal.map_whiskerLeft_assoc, Functor.Monoidal.map_whiskerRight_assoc,
        Functor.Monoidal.Œº_Œ¥_assoc, whisker_exchange_assoc, ‚Üê comp_whiskerRight_assoc,
        Iso.map_inv_hom_id, Category.comp_id, ‚Üê whisker_exchange_assoc,
        Functor.LaxMonoidal.Œº_whiskerRight_comp_Œº_assoc, Iso.map_inv_hom_id_assoc,
        Functor.Monoidal.Œº_Œ¥_assoc, normalization_of, associator_naturality_right_assoc,
        Iso.inv_hom_id_assoc]
      simp only [‚Üê Category.assoc, cancel_mono]; simp only [Category.assoc]
      -- and now it‚Äôs all strict norphisms so we can just rip it apart.
      simp [show (Œª_ _) = SList.leftUnitor _ by rfl,
        show (Œ±_ _ _ _) = SList.associator _ _ _ by rfl,
        SList.leftUnitor, tensorObjConsIso, appendNilIso, unitIsoNil, SList.associator, eqToHom_map]

instance : (unitIso C).inv.IsMonoidal where
  unit := by
    rw [‚Üê IsIso.inv_eq_inv]
    have := NatTrans.IsMonoidal.unit (œÑ := (unitIso C).hom)
    simp only [Functor.comp_obj, Functor.id_obj, Functor.LaxMonoidal.id_Œµ, Category.id_comp,
      Functor.LaxMonoidal.comp_Œµ] at this
    simp [‚Üê Functor.map_inv, this]
  tensor x y := by
    rw [‚Üê IsIso.inv_eq_inv]
    have := NatTrans.IsMonoidal.tensor (œÑ := (unitIso C).hom) x y
    simp only [Functor.id_obj, Functor.comp_obj, Functor.LaxMonoidal.id_Œº, Category.id_comp,
      Functor.LaxMonoidal.comp_Œº] at this
    simp [‚Üê Functor.map_inv, this]

/-- The natural isomorphism `Œµ : K ‚ãô J ‚âÖ ùü≠` on `FreeSMC C`:
it is defined via the universal property of the free symmetric monoidal category,
as both sides are monoidal. -/
public def counitIso : normalization C ‚ãô J C ‚âÖ ùü≠ (FreeSymmetricMonoidalCategory C) :=
  FreeSymmetricMonoidalCategory.liftNatIso fun c ‚Ü¶
    JObjConsIso c _ ‚â™‚â´ whiskerLeftIso (of c) JObjNilIso ‚â™‚â´ œÅ_ _

@[simp, reassoc]
lemma counitIso_hom_app_unit :
    (counitIso C).hom.app (ùüô_ (FreeSymmetricMonoidalCategory C)) =
      (J C).map (Functor.OplaxMonoidal.Œ∑ (normalization C)) ‚â´ Functor.OplaxMonoidal.Œ∑ (J C) := by
  simp [counitIso]

@[simp, reassoc]
lemma counitIso_inv_app_unit :
    (counitIso C).inv.app (ùüô_ (FreeSymmetricMonoidalCategory C)) =
      Functor.LaxMonoidal.Œµ (J C) ‚â´ (J C).map (Functor.LaxMonoidal.Œµ (normalization C)) := by
  simp [counitIso]

@[simp]
lemma counitIso_hom_app_of (c : C) :
    (counitIso C).hom.app (of c) =
    (JObjConsIso c _).hom ‚â´ (of c) ‚óÅ JObjNilIso.hom ‚â´ (œÅ_ _).hom := by
  simp [counitIso]

@[simp]
lemma counitIso_inv_app_of (c : C) :
    (counitIso C).inv.app (of c) =
    (œÅ_ _).inv ‚â´ (of c) ‚óÅ JObjNilIso.inv ‚â´ (JObjConsIso c _).inv := by
  simp [counitIso]

@[simp, reassoc]
lemma counitIso_hom_app_tensor (x y : FreeSymmetricMonoidalCategory C) :
    (counitIso C).hom.app (x ‚äó y) =
    (J C).map (Functor.OplaxMonoidal.Œ¥ (normalization C) x y) ‚â´
    Functor.OplaxMonoidal.Œ¥ (J C) ((normalization C).obj x) ((normalization C).obj y) ‚â´
      (((counitIso C).hom.app x) ‚äó‚Çò ((counitIso C).hom.app y)) := by
    simp [counitIso]

@[simp, reassoc]
lemma counitIso_inv_app_tensor (x y : FreeSymmetricMonoidalCategory C) :
    (counitIso C).inv.app (x ‚äó y) =
    (((counitIso C).inv.app x) ‚äó‚Çò ((counitIso C).inv.app y)) ‚â´
      Functor.LaxMonoidal.Œº (J C) ((normalization C).obj x) ((normalization C).obj y) ‚â´
        (J C).map (Functor.LaxMonoidal.Œº (normalization C) x y) := by
  simp [counitIso]

instance : (counitIso C).hom.IsMonoidal where
instance : (counitIso C).inv.IsMonoidal where

/-- The coherence theorem: `SList C` is equivalent to `FreeSymmetricMonoidalCategory C`
as a symmetric monoidal category. -/
@[simps, expose]
public def equivalence : SList C ‚âå FreeSymmetricMonoidalCategory C where
  functor := J C
  inverse := normalization C
  unitIso := unitIso C
  counitIso := counitIso C
  functor_unitIso_comp x := by
    dsimp
    induction x using SList.cons_induction with
    | nil =>
      simp only [unitIso, Functor.id_obj, Functor.comp_obj, Functor.mapIso_symm,
        Functor.mapIso_trans, Iso.trans_assoc, recNatIso_hom_app_nil, Iso.trans_hom, Iso.symm_hom,
        Functor.Monoidal.ŒµIso_hom, Functor.CoreMonoidal.toMonoidal_toLaxMonoidal,
        Functor.mapIso_inv, Functor.map_comp, Category.assoc]
      rw [‚Üê cancel_mono JObjNilIso.hom]
      have := (counitIso C).hom.naturality JObjNilIso.hom
      dsimp at this
      simp only [‚Üê Functor.map_comp_assoc, Category.assoc, ‚Üê this, ‚Üê Functor.map_comp,
        Iso.inv_hom_id, Functor.map_id, Category.comp_id, Category.id_comp]
      simp [counitIso, J_Œ∑]
    | cons c l hr =>
      /- We use the computation rules for J to get terms of the form `of c ‚äó (J C).obj l` in the
      counit, that we can then compute via counitIso_hom_app_tensor -/
      have nat‚ÇÅ := (counitIso C).hom.naturality (JObjConsIso c l).hom
      simp only [Functor.comp_obj, Functor.id_obj, Functor.comp_map, counitIso_hom_app_tensor,
        normalization_of, Functor.CoreMonoidal.toMonoidal_toOplaxMonoidal, counitIso_hom_app_of,
        Functor.id_map] at nat‚ÇÅ
      simp_rw [‚Üê cancel_mono (JObjConsIso c l).hom, Category.assoc, ‚Üê nat‚ÇÅ]
      /- Now unitIso_hom_app_cons gives also a computation for (unitIso C).hom.app (c ::~ l) in
      terms of (unitIso C).hom.app l -/
      simp only [unitIso_hom_app_cons, Functor.comp_obj, Functor.map_comp, tensorHom_def,
        comp_whiskerRight, whisker_assoc, Category.assoc, triangle_assoc_comp_right,
        Category.id_comp]
      /- Now the idea is to slowly push the term involving (J C).map (unitIso C).hom.app l to
      the bottom of the expression (where there is a (counitIso C).hom.app ((J C).obj l),
      so that we can cancel them using the induction hypothesis, this is a lot of
      monoidal rewriting. -/
      simp_rw [‚Üê whiskerLeft_comp_assoc, ‚Üê whiskerLeft_comp, ‚Üê Functor.map_comp_assoc,
        Iso.inv_hom_id, Functor.map_id, Category.id_comp, Functor.Monoidal.Œº_Œ¥]
      simp only [Functor.map_comp, normalization_of, Category.comp_id, Category.assoc,
        J_map_cons_map, Iso.inv_hom_id_assoc, whiskerLeft_comp]
      simp_rw [Functor.Monoidal.map_whiskerRight, whiskerLeft_comp_assoc,
        Functor.Monoidal.map_whiskerLeft_assoc, Functor.Monoidal.Œº_Œ¥_assoc, whisker_exchange_assoc,
        whisker_assoc_symm_assoc, Iso.hom_inv_id_assoc, whisker_exchange_assoc,
        associator_naturality_right_assoc]
      simp_rw [‚Üê whiskerLeft_comp_assoc, ‚Üê whiskerLeft_comp, leftUnitor_naturality, Category.assoc]
      /- we can finaly use the induction hyp: -/
      rw [hr]
      /- and now everything that remains should cancel out,
      though this is going to be a bit slow. -/
      rw [‚Üê cancel_epi ((JObjConsIso c l).inv)]
      simp only [Functor.CoreMonoidal.toMonoidal_toOplaxMonoidal, whiskerLeft_comp, whisker_assoc,
        Functor.CoreMonoidal.toMonoidal_toLaxMonoidal, Category.comp_id, triangle, Category.assoc,
        triangle_assoc_comp_right, Iso.inv_hom_id_assoc, Iso.inv_hom_id]
      simp_rw [whisker_assoc_symm_assoc, Iso.hom_inv_id_assoc,
        ‚Üê Functor.Monoidal.inv_Œº, J_Œº_app_cons_app]
      simp only [Functor.CoreMonoidal.toMonoidal_toLaxMonoidal, Functor.Monoidal.inv_Œº,
        Functor.CoreMonoidal.toMonoidal_toOplaxMonoidal, whisker_assoc, IsIso.inv_comp,
        IsIso.Iso.inv_inv, inv_whiskerLeft, Category.assoc, IsIso.Iso.inv_hom, inv_whiskerRight,
        triangle, triangle_assoc_comp_right, inv_hom_whiskerRight_assoc, Iso.inv_hom_id_assoc,
        IsIso.hom_inv_id_assoc, Functor.Monoidal.whiskerLeft_Œº_Œ¥_assoc]
      simp_rw [‚Üê whiskerLeft_comp_assoc, ‚Üê whiskerLeft_comp]
      simp only [Functor.OplaxMonoidal.Œ¥_natural_left_assoc, Category.assoc, whiskerLeft_comp]
      simp_rw [‚Üê Functor.Monoidal.inv_Œº, J_Œº_app_nil_app]
      simp only [Functor.map_comp, IsIso.inv_comp, IsIso.Iso.inv_hom,
        Functor.OplaxMonoidal.left_unitality, Functor.CoreMonoidal.toMonoidal_toOplaxMonoidal,
        Category.assoc, IsIso.inv_hom_id_assoc, inv_whiskerRight, whiskerLeft_comp,
        whiskerLeft_hom_inv'_assoc]
      simp_rw [‚Üê whiskerLeft_comp_assoc, ‚Üê whiskerLeft_comp]
      simp


public instance : (equivalence C).functor.Monoidal := inferInstanceAs (J C).Monoidal
public instance : (equivalence C).functor.Braided := inferInstanceAs (J C).Braided
public instance : (equivalence C).inverse.Monoidal := inferInstanceAs (normalization C).Monoidal
public instance : (equivalence C).inverse.Braided := inferInstanceAs (normalization C).Braided

public instance : (equivalence C).IsMonoidal where
  leftAdjoint_Œµ := by
    dsimp [equivalence]
    have := NatTrans.IsMonoidal.unit (œÑ := (unitIso C).hom)
    simp at this
    simp [this]
  leftAdjoint_Œº x y := by
    dsimp [equivalence]
    have u‚ÇÅ := NatTrans.IsMonoidal.tensor (œÑ := (unitIso C).hom) ((normalization C).obj x)
      (normalization C |>.obj y)
    have u‚ÇÇ := NatTrans.IsMonoidal.tensor (œÑ := (counitIso C).hom) x y
    simp only [Functor.id_obj, Functor.comp_obj, Functor.LaxMonoidal.id_Œº, Category.id_comp,
      Functor.LaxMonoidal.comp_Œº] at u‚ÇÅ
    dsimp at u‚ÇÇ
    simp only [Category.assoc, Category.comp_id] at u‚ÇÇ
    simp only [‚Üê u‚ÇÇ, Functor.Monoidal.Œ¥_Œº_assoc, u‚ÇÅ, Category.assoc]
    have := (equivalence C).unit_app_inverse
    dsimp [equivalence] at this
    simp_rw [this, Functor.LaxMonoidal.Œº_natural_assoc, ‚Üê Functor.map_comp]
    simp

public instance : (equivalence C).unit.IsMonoidal := inferInstance
public instance : (equivalence C).unitInv.IsMonoidal := inferInstance
public instance : (equivalence C).counit.IsMonoidal := inferInstance
public instance : (equivalence C).counitInv.IsMonoidal := inferInstance

end Equivalence

section UniversalProperty

/-! ## (Symmetric monoidal) universal property of symmetric lists
In this section, we transfer the universal property of FreeSymmetricMonoidalCategory to
symmetric lists. -/

variable {C} {D : Type*} [Category* D] [MonoidalCategory D] [SymmetricCategory D]

/-- The (symmetric monoidal) functor `SList C ‚•§ D` induced by a function C ‚Üí D when
`D` is a symmetric monoidal category. -/
@[expose] public def monoidalLift (p : C ‚Üí D) : SList C ‚•§ D :=
  (equivalence C).functor ‚ãô FreeSymmetricMonoidalCategory.project p

public instance (p : C ‚Üí D) : (monoidalLift p).Monoidal :=
    inferInstanceAs ((equivalence C).functor ‚ãô FreeSymmetricMonoidalCategory.project p).Monoidal

public instance (p : C ‚Üí D) : (monoidalLift p).Braided :=
    inferInstanceAs ((equivalence C).functor ‚ãô FreeSymmetricMonoidalCategory.project p).Braided

@[expose] public def monoidalLiftConsNilIso (p : C ‚Üí D) (c : C) :
    (monoidalLift p).obj [c]~ ‚âÖ p c :=
  (project p).mapIso (JObjConsIso c []~) ‚â™‚â´
  (Functor.Monoidal.ŒºIso (project p) _ _).symm ‚â™‚â´
    whiskerLeftIso _ ((project p).mapIso JObjNilIso) ‚â™‚â´
    whiskerLeftIso _ (Functor.Monoidal.ŒµIso _).symm ‚â™‚â´ œÅ_ _

/-- Given two symmetric monoidal functors `F G : SList C ‚•§ D`, a family of morphisms
`F.obj [c]~ ‚Üí G.obj [c]~` determines a monoidal natural transformation from `F` to `G`. -/
@[expose] public def monoidalLiftNatTrans
    {F G : SList C ‚•§ D} [F.Braided] [G.Braided]
    (œÜ : ‚àÄ (c : C), F.obj [c]~ ‚ü∂ G.obj [c]~) :
    F ‚ü∂ G :=
  letI F' : FreeSymmetricMonoidalCategory C ‚•§ D := (equivalence C).inverse ‚ãô F
  letI G' : FreeSymmetricMonoidalCategory C ‚•§ D := (equivalence C).inverse ‚ãô G
  letI œÜ' : ‚àÄ (c : C), F'.obj (of c) ‚ü∂ G'.obj (of c) := œÜ
  letI Œ±‚ÇÄ : F' ‚ü∂ G' := FreeSymmetricMonoidalCategory.liftNatTrans œÜ'
  (Functor.leftUnitor _).inv ‚â´
    Functor.whiskerRight (equivalence C).unit _ ‚â´
    (Functor.associator ..).hom ‚â´
    Functor.whiskerLeft (equivalence C).functor Œ±‚ÇÄ ‚â´
    (Functor.associator ..).inv ‚â´
    Functor.whiskerRight (equivalence C).unitInv _ ‚â´
    (Functor.leftUnitor _).hom

public section

variable {C : Type*} [Category* C] [MonoidalCategory C]
  {D : Type*} [Category* D] [MonoidalCategory D]
  {E : Type*} [Category* E] [MonoidalCategory E]
  {E' : Type*} [Category* E'] [MonoidalCategory E']

variable {F‚ÇÅ F‚ÇÇ F‚ÇÉ : C ‚•§ D} (œÑ : F‚ÇÅ ‚ü∂ F‚ÇÇ) [F‚ÇÅ.LaxMonoidal] [F‚ÇÇ.LaxMonoidal] [F‚ÇÉ.LaxMonoidal]

open NatTrans
instance {G‚ÇÅ : D ‚•§ E} [G‚ÇÅ.LaxMonoidal] [IsMonoidal œÑ] :
    IsMonoidal (Functor.whiskerRight œÑ G‚ÇÅ) := by
  rw [‚Üê Functor.hcomp_id]
  infer_instance

instance {G‚ÇÅ G‚ÇÇ : D ‚•§ E} [G‚ÇÅ.LaxMonoidal] [G‚ÇÇ.LaxMonoidal]
    (œÑ' : G‚ÇÅ ‚ü∂ G‚ÇÇ) [IsMonoidal œÑ'] :
    IsMonoidal (Functor.whiskerLeft F‚ÇÅ œÑ') := by
  rw [‚Üê Functor.id_hcomp]
  infer_instance

end

public instance {F G : SList C ‚•§ D} [F.Braided] [G.Braided]
    (œÜ : ‚àÄ (c : C), F.obj [c]~ ‚ü∂ G.obj [c]~) : (monoidalLiftNatTrans œÜ).IsMonoidal := by
  dsimp [monoidalLiftNatTrans]
  -- The composition is a bit too big, so we have to provide an intermediate instance
  letI F' : FreeSymmetricMonoidalCategory C ‚•§ D := (equivalence C).inverse ‚ãô F
  letI G' : FreeSymmetricMonoidalCategory C ‚•§ D := (equivalence C).inverse ‚ãô G
  letI œÜ' : ‚àÄ (c : C), F'.obj (of c) ‚ü∂ G'.obj (of c) := œÜ
  letI Œ±‚ÇÄ : F' ‚ü∂ G' := FreeSymmetricMonoidalCategory.liftNatTrans œÜ'
  haveI : NatTrans.IsMonoidal
    ((J C).whiskerLeft (FreeSymmetricMonoidalCategory.liftNatTrans œÜ') ‚â´
          ((J C).associator (normalization C) G).inv ‚â´
            Functor.whiskerRight (equivalence C).unitInv G ‚â´ G.leftUnitor.hom) :=
    inferInstance
  infer_instance

public section

variable {F G : SList C ‚•§ D} [F.Braided] [G.Braided] (œÜ : ‚àÄ (c : C), F.obj [c]~ ‚ü∂ G.obj [c]~)

@[simp]
lemma monoidalLiftNatTrans_app_singleton (c : C) : (monoidalLiftNatTrans œÜ).app ([c]~) = œÜ c := by
  letI F' : FreeSymmetricMonoidalCategory C ‚•§ D := (equivalence C).inverse ‚ãô F
  letI G' : FreeSymmetricMonoidalCategory C ‚•§ D := (equivalence C).inverse ‚ãô G
  letI œÜ' : ‚àÄ (c : C), F'.obj (of c) ‚ü∂ G'.obj (of c) := œÜ
  letI Œ±‚ÇÄ : F' ‚ü∂ G' := FreeSymmetricMonoidalCategory.liftNatTrans œÜ'
  simp only [monoidalLiftNatTrans, equivalence_functor, equivalence_inverse, NatTrans.comp_app,
    Functor.comp_obj, Functor.id_obj, Functor.leftUnitor_inv_app, Functor.whiskerRight_app,
    Functor.associator_hom_app, Functor.whiskerLeft_app, Functor.associator_inv_app,
    Functor.leftUnitor_hom_app, Category.comp_id, Category.id_comp]
  change F.map ((equivalence C).unit.app [c]~) ‚â´
    Œ±‚ÇÄ.app ((J C).obj [c]~) ‚â´ G.map ((equivalence C).unitInv.app [c]~) = œÜ c
  let nat‚ÇÅ := Œ±‚ÇÄ.naturality (JObjConsIso c []~ ‚â™‚â´ whiskerLeftIso (of c) JObjNilIso ‚â™‚â´ œÅ_ _).hom
  simp [-NatTrans.naturality, G', F'] at nat‚ÇÅ
  have := (equivalence C).unitInv_app_inverse ((of c))
  dsimp at this
  rw [this]
  dsimp [equivalence]
  rw [unitIso_hom_app_singleton]
  simp only [Functor.map_comp, normalization_of, equivalence_inverse, counitIso_hom_app_of, ‚Üê nat‚ÇÅ,
    liftNatTrans_app_of, Category.assoc, F', Œ±‚ÇÄ, œÜ']
  simp [‚Üê Functor.map_comp, ‚Üê Functor.map_comp_assoc]

lemma monoidalNatTrans_ext_app_singleton {Œ± Œ≤ : F ‚ü∂ G} [Œ±.IsMonoidal] [Œ≤.IsMonoidal]
    (h : ‚àÄ c : C, Œ±.app [c]~ = Œ≤.app [c]~) : Œ± = Œ≤ := by
  apply (Functor.whiskeringLeft .. |>.obj ((equivalence C).inverse)).map_injective
  dsimp
  apply FreeSymmetricMonoidalCategory.ext_of_monoidal
  exact h

@[expose, simps] public def monoidalLiftNatIso
    {F G : SList C ‚•§ D} [F.Braided] [G.Braided]
    (œÜ : ‚àÄ (c : C), F.obj [c]~ ‚âÖ G.obj [c]~) :
    F ‚âÖ G where
  hom := monoidalLiftNatTrans fun c ‚Ü¶ (œÜ c).hom
  inv := monoidalLiftNatTrans fun c ‚Ü¶ (œÜ c).inv
  hom_inv_id := by
    apply monoidalNatTrans_ext_app_singleton
    simp
  inv_hom_id := by
    apply monoidalNatTrans_ext_app_singleton
    simp

public instance {F G : SList C ‚•§ D} [F.Braided] [G.Braided]
    (œÜ : ‚àÄ (c : C), F.obj [c]~ ‚âÖ G.obj [c]~) : (monoidalLiftNatIso œÜ).hom.IsMonoidal :=
  inferInstanceAs <| NatTrans.IsMonoidal <| monoidalLiftNatTrans ..

end

  -- letI F' : FreeSymmetricMonoidalCategory C ‚•§ D := (equivalence C).inverse ‚ãô F
  -- letI G' : FreeSymmetricMonoidalCategory C ‚•§ D := (equivalence C).inverse ‚ãô G
  -- letI œÜ' : ‚àÄ (c : C), F'.obj (of c) ‚ü∂ G'.obj (of c) := œÜ
  -- letI Œ±‚ÇÄ : F' ‚ü∂ G' := FreeSymmetricMonoidalCategory.liftNatTrans œÜ'
  -- (Functor.leftUnitor _).inv ‚â´
  --   Functor.whiskerRight (equivalence C).unit _ ‚â´
  --   (Functor.associator ..).hom ‚â´
  --   Functor.whiskerLeft (equivalence C).functor Œ±‚ÇÄ ‚â´
  --   (Functor.associator ..).inv ‚â´
  --   Functor.whiskerRight (equivalence C).unitInv _ ‚â´
  --   (Functor.leftUnitor _).hom


end UniversalProperty

end CategoryTheory.SList
