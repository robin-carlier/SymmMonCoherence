/-
Copyright (c) 2026 Robin Carlier. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Robin Carlier
-/
module

public import SymmMonCoherence.SList.EffBurnside
public import SymmMonCoherence.SList.Substitution
public import SymmMonCoherence.SList.Pseudofunctor
public import SymmMonCoherence.SList.Duality
public import Mathlib.CategoryTheory.Limits.Shapes.Pullback.IsPullback.Basic

universe u

@[expose] public section

/-! # The Kleisli bicategory of symmetric lists

In this file, we introduce the Kleisli bicategory of symmetric lists,
i.e the bicategory whose objects are types, and arrows
J -> K are functions K -> SList J (notice the reversal in direction).

This category is a tool to abstract the monoidal computations required to build
a pseudo-functor Burnside (FintypeCat) ‚•§·µñ Cat out of a symmetric monoidal category:
we will show that any symmetric monoidal category in fact defines an object
Kleisli ‚•§·µñ Cat first. Then, the natural target for the functor out of
`Burnside (FintypeCat)` will be Kleisli instead of `Cat`: this fully encodes the
idea that "all of the formulas involved are universal": in practice, this will
lessen the back and forth between the category we interpret the formulas in and
the category of symmetric lists.
-/

namespace CategoryTheory.SList

namespace RelativePseudomonad

/-! In this section, we give the assignment `X ‚Ü¶ SList X` the structure of a relative pseudomonad
over the inclusion `Type ‚•§·µñ Cat` (though we state it in an unbundled way).
This is mostly a sanity check. -/

/-- The structure isomorphism (g^* f)^* ‚ü∂ g^*f^* of the relative pseudomonad
of symmetric lists, we generalize the target of `g` to be any symmetric monoidal category
rather than just a free one, as this is useful. -/
def Œº {X Y Z : Type*} [Category* Z] [MonoidalCategory Z] [SymmetricCategory Z]
    (g : Y ‚Üí Z) (f : X ‚Üí SList Y) :
    monoidalLift ((Pi.precompFunctor' _ f).obj (monoidalLift g)) ‚âÖ
    monoidalLift f ‚ãô monoidalLift g :=
  monoidalLiftNatIso (fun x ‚Ü¶
    monoidalLiftConsNilIso ((Pi.precompFunctor' Z f).obj (monoidalLift g)) x ‚â™‚â´
      (monoidalLift g).mapIso (monoidalLiftConsNilIso f x).symm)

section

variable {X Y Z : Type*} [Category* Z] [MonoidalCategory Z] [SymmetricCategory Z]
  (g : Y ‚Üí Z) (f : X ‚Üí SList Y)

instance : (Œº g f).hom.IsMonoidal := by
  dsimp [Œº]
  infer_instance

@[simp]
lemma Œº_hom_app_singleton
    (x : X) :
    (Œº g f).hom.app [x]~ =
    (monoidalLiftConsNilIso ((Pi.precompFunctor' Z f).obj (monoidalLift g)) x).hom ‚â´
      (monoidalLift g).map (monoidalLiftConsNilIso f x).inv := by
  simp [Œº]

@[simp]
lemma Œº_inv_app_singleton (x : X) :
    (Œº g f).inv.app [x]~ =
    (monoidalLift g).map (monoidalLiftConsNilIso f x).hom ‚â´
      (monoidalLiftConsNilIso
        ((Pi.precompFunctor' Z f).obj (monoidalLift g)) x).inv := by
  simp [Œº]

end

variable {X Y Z : Type*} (f : X ‚Üí SList Y) (g : Y ‚Üí SList Z)

abbrev Œπ (X : Type*) (x : X) : SList X := [x]~

abbrev Œ∑ {X : Type*} (f : X ‚Üí SList Y) :
    f ‚âÖ (Pi.precompFunctor' _ (Œπ X)).obj (monoidalLift f) :=
  Pi.isoMk (fun c ‚Ü¶ (monoidalLiftConsNilIso f c).symm)

abbrev Œ∏ (X : Type*) :
    monoidalLift (Œπ X) ‚âÖ ùü≠ (SList X) :=
  monoidalLiftNatIso (fun _ ‚Ü¶ (monoidalLiftConsNilIso ..))

lemma assoc {X Y Z V : Type*} (f : X ‚Üí SList Y) (g : Y ‚Üí SList Z) (h : Z ‚Üí SList V) :
    (monoidalLiftFunctor ..).map ((Pi.precompFunctor' _ f).map (Œº h g).hom) ‚â´
      (monoidalLiftFunctor ..).map (Pi.precompFunctor'AssocIso ..).hom ‚â´
      (Œº _ _).hom ‚â´
      Functor.whiskerRight (Œº _ _).hom ((monoidalLiftFunctor ..).obj h) =
    (Œº _ _).hom ‚â´ (Functor.whiskerLeft (((monoidalLiftFunctor ..).obj f)) (Œº _ _).hom) ‚â´
      (Functor.associator _ _ _).inv := by
  apply monoidalNatTrans_ext_app_singleton
  intro c
  simp

lemma assoc' {X Y Z V : Type*} [Category* V] [MonoidalCategory V] [SymmetricCategory V]
    (f : X ‚Üí SList Y) (g : Y ‚Üí SList Z) (h : Z ‚Üí V) :
    (monoidalLiftFunctor ..).map ((Pi.precompFunctor' _ f).map (Œº h g).hom) ‚â´
      (monoidalLiftFunctor ..).map (Pi.precompFunctor'AssocIso ..).hom ‚â´
      (Œº _ _).hom ‚â´
      Functor.whiskerRight (Œº _ _).hom ((monoidalLiftFunctor ..).obj h) =
    (Œº _ _).hom ‚â´ (Functor.whiskerLeft (((monoidalLiftFunctor ..).obj f)) (Œº _ _).hom) ‚â´
      (Functor.associator _ _ _).inv := by
  apply monoidalNatTrans_ext_app_singleton
  intro c
  simp

lemma unit {X Y : Type*} (f : X ‚Üí SList Y) :
    (monoidalLiftFunctor ..).map (Œ∑ f).hom ‚â´ (Œº f (Œπ X)).hom ‚â´
      Functor.whiskerRight (Œ∏ X).hom ((monoidalLiftFunctor ..).obj f) =
    (Functor.leftUnitor _).inv := by
  apply monoidalNatTrans_ext_app_singleton
  intro c
  simp

/- Yay, we have everything needed to show it‚Äôs a relative pseudomonad. -/

--   inferInstanceAs <| (monoidalSubst K (SList L)).Monoidal
--
-- instance (K L : Type*) : (Œ¶ K L).Braided :=
--   inferInstanceAs <| (monoidalSubst K (SList L)).Braided

end RelativePseudomonad

@[pp_with_univ]
structure Kleisli where
  of : Type u

namespace Kleisli

/- The Hom-types in Kleisli: a morphism `X ‚ü∂ Y` is
a function `Y.of ‚Üí SList X.of`: notice that the direction is
backwards: this is so that the pseudofunctor we will construct later
lands in `Cat` instead of `Cat·µí·µñ`. -/
structure Hom (X Y : Kleisli.{u}) where
  of : Y.of ‚Üí SList X.of

instance : Quiver Kleisli where
  Hom := Hom

@[simps! -fullyApplied]
instance homCategory (X Y : Kleisli.{u}) : Category.{u} (X ‚ü∂ Y) :=
  inferInstanceAs (Category <| InducedCategory _ (¬∑.of))

@[ext]
lemma hom‚ÇÇ_ext {X Y : Kleisli.{u}} {f g : X ‚ü∂ Y} {Œ∑ Œ∏ : f ‚ü∂ g} (h : Œ∑.hom = Œ∏.hom) :
    Œ∑ = Œ∏ := by
  apply InducedCategory.hom_ext
  exact h

@[simps! -fullyApplied id_of comp_of]
instance : CategoryStruct Kleisli.{u} where
  comp f g := .mk <| (Pi.precompFunctor'.{u} _ g.of).obj ((monoidalLiftFunctor ..).obj f.of)
  id _ := .mk (RelativePseudomonad.Œπ _)

@[simps]
abbrev mkIso‚ÇÇ {X Y : Kleisli.{u}} {f g : X ‚ü∂ Y} (e : f.of ‚âÖ g.of) : f ‚âÖ g where
  hom := .mk e.hom
  inv := .mk e.inv
  hom_inv_id := by
    ext : 1
    simp
  inv_hom_id := by
    ext : 1
    simp

abbrev associator {X Y Z T : Kleisli.{u}} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) (h : Z ‚ü∂ T) :
    (f ‚â´ g) ‚â´ h ‚âÖ f ‚â´ g ‚â´ h :=
  mkIso‚ÇÇ <|
    ((Pi.precompFunctor' (SList X.of) h.of).mapIso (RelativePseudomonad.Œº f.of g.of)) ‚â™‚â´
      Pi.precompFunctor'AssocIso ..

abbrev rightUnitor {X Y : Kleisli.{u}} (f : X ‚ü∂ Y) : (f ‚â´ ùüô _) ‚âÖ f :=
  mkIso‚ÇÇ <| (RelativePseudomonad.Œ∑ _).symm

abbrev leftUnitor {X Y : Kleisli.{u}} (f : X ‚ü∂ Y) :
    ùüô _ ‚â´ f ‚âÖ f :=
  mkIso‚ÇÇ <| (Pi.precompFunctor' ..).mapIso (RelativePseudomonad.Œ∏ _) ‚â™‚â´
    Pi.precompFunctor'Id _

@[simps! whiskerRight_hom whiskerLeft_hom associator_hom_hom associator_inv_hom
rightUnitor_hom_hom rightUnitor_inv_hom leftUnitor_hom_hom leftUnitor_inv_hom]
instance : Bicategory Kleisli.{u} where
  homCategory := homCategory.{u}
  whiskerLeft {X Y Z} f {g h} Œ∑ :=
    .mk <| fun z ‚Ü¶
      ((monoidalLiftFunctor ..).obj f.of).map (Œ∑.hom z)
  whiskerRight {X Y Z} {f g} Œ∑ h :=
    .mk <| fun z ‚Ü¶
      ((monoidalLiftFunctor ..).map (Œ∑.hom)).app (h.of z)
  associator f g h := associator f g h
  leftUnitor := leftUnitor
  rightUnitor := rightUnitor
  comp_whiskerLeft {a b c d} f g {h h'} Œ∑ := by
    ext i
    dsimp
    have := (RelativePseudomonad.Œº f.of g.of).hom.naturality (Œ∑.hom i)
      =‚â´ (RelativePseudomonad.Œº f.of g.of).inv.app (h'.of i)
    simp only [Category.assoc, Iso.hom_inv_id_app, Category.comp_id] at this
    simpa using this
  comp_whiskerRight {a b c} {f g h} Œ∑ Œ∏ i := by
    ext i
    dsimp
    simp_rw [‚Üê NatTrans.comp_app]
    congr 1
    apply monoidalNatTrans_ext_app_singleton
    intro c
    simp
  id_whiskerLeft {a b} f g Œ∑ := by
    ext i
    dsimp
    simp only [Category.comp_id, Category.id_comp]
    let e : monoidalLift (RelativePseudomonad.Œπ a.of) ‚âÖ ùü≠ _ := RelativePseudomonad.Œ∏ _
    have := e.hom.naturality (Œ∑.hom i) =‚â´ e.inv.app (g.of i)
    simp only [Functor.id_obj, Category.assoc, Iso.hom_inv_id_app, Category.comp_id,
      Functor.id_map] at this
    rw [this]
    congr
  id_whiskerRight {a b c} f g := by
    ext i
    dsimp
    simp only [Category.id_comp, Iso.hom_inv_id]
    suffices H : monoidalLiftNatTrans (fun k ‚Ü¶ ùüô ((monoidalLift f.of).obj [k]~)) = ùüô _ by
      rw [H]
      simp
    apply monoidalNatTrans_ext_app_singleton
    intro c
    simp
  whiskerRight_comp {a b c d} {f g} Œ∑ h k := by
    ext i
    dsimp
    simp only [Category.id_comp, Category.comp_id]
    simp_rw [‚Üê NatTrans.comp_app_assoc, ‚Üê NatTrans.comp_app]
    rw [‚Üê Functor.whiskerLeft_app]
    congr 1
    apply monoidalNatTrans_ext_app_singleton
    intro c
    simp
  whisker_assoc {a b c d} f g h Œ∑ k := by
    ext i
    dsimp
    simp only [Category.comp_id, Category.id_comp]
    simp_rw [‚Üê Functor.whiskerRight_app, ‚Üê NatTrans.comp_app_assoc, ‚Üê NatTrans.comp_app]
    congr 1
    apply monoidalNatTrans_ext_app_singleton
    intro c
    simp
  pentagon {a b c d e} f g h k := by
    ext i
    dsimp
    simp only [Category.comp_id]
    simp_rw [‚Üê Functor.whiskerRight_app, ‚Üê Functor.whiskerLeft_app, ‚Üê NatTrans.comp_app_assoc,
      ‚Üê NatTrans.comp_app]
    congr 1
    /- Rewriting `Category.comp_id` followed by `congr` as we did above yields an expression that
    abuses associativity of functor composition (one of the identities that
    got rewritten was secretly a `Functor.associator ..`).
    Resolving the abuse as below is a bit ugly but still faster than trying to avoid the
    rewriting of that identity (which would involve precisely identifying from which
    associator every identity comes from, using nth-rewrite to only rewrite this one and
    not the other etc). -/
    apply (config := {allowSynthFailures := true}) monoidalNatTrans_ext_app_singleton
    ¬∑ apply (config := {allowSynthFailures := true}) NatTrans.IsMonoidal.comp
      haveI : NatTrans.IsMonoidal <|
        (monoidalLift h.of).whiskerLeft (RelativePseudomonad.Œº f.of g.of).hom := inferInstance
      convert this
      ext <;> simp [Functor.LaxMonoidal.comp]
    ¬∑ intro c
      simp
  triangle {f g h} x y := by
    ext i
    dsimp
    simp only [Iso.hom_inv_id, Functor.map_comp, ‚Üê Functor.whiskerRight_app]
    rw [Functor.map_id (monoidalLift x.of)]
    nth_rw 2 [‚Üê Functor.leftUnitor_hom_app (monoidalLift x.of)]
    simp_rw [Category.comp_id, ‚Üê NatTrans.comp_app]
    congr 1
    apply monoidalNatTrans_ext_app_singleton
    intro c
    simp

universe v' u'
open Bicategory

/-- The action of a morphism in the Kleisli bicategory on a symmetric monoidal category. -/
abbrev extendedPullback (C : Type u') [Category.{v'} C]
    [MonoidalCategory C] [SymmetricCategory C] {X Y : Kleisli.{u}}
    (f : X ‚ü∂ Y) : (X.of ‚Üí C) ‚•§ (Y.of ‚Üí C) :=
  monoidalLiftFunctor _ _ ‚ãô Pi.precompFunctor' _ f.of

/-- The action of a 2-morphism in the Kleisli bicategory on a symmetric monoidal category. -/
abbrev extendedPullback‚ÇÇ (C : Type u') [Category.{v'} C]
    [MonoidalCategory C] [SymmetricCategory C] {X Y : Kleisli.{u}}
    {f g : X ‚ü∂ Y} (Œ∑ : f ‚ü∂ g) : extendedPullback C f ‚ü∂ extendedPullback C g :=
  (monoidalLiftFunctor _ _).whiskerLeft ((Pi.precomposingFunctor C _ _).map Œ∑.hom)

example (C : Type u') [Category.{v'} C]
    [MonoidalCategory C] [SymmetricCategory C] {X Y : Kleisli.{u}}
    {f g : X ‚ü∂ Y} (Œ∑ : f ‚ü∂ g) : (extendedPullback‚ÇÇ C Œ∑).IsMonoidal := by infer_instance

abbrev extendedPullbackComp (C : Type*) [Category* C] [MonoidalCategory C] [SymmetricCategory C]
    {X Y Z : Kleisli.{u}}
    (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) :
    extendedPullback C (f ‚â´ g) ‚âÖ extendedPullback C f ‚ãô extendedPullback C g :=
  NatIso.ofComponents (fun X ‚Ü¶ Pi.isoMk (fun i ‚Ü¶ (RelativePseudomonad.Œº _ _).symm.app (g.of i)))
    (fun {U V} Œ∑ ‚Ü¶ by
      ext k
      simp only [Functor.comp_obj, comp_of, monoidalLiftFunctor_obj, Pi.precompFunctor'_obj,
        Functor.comp_map, Pi.comp_apply,
        Pi.precompFunctor'_map, Pi.isoMk_hom, Iso.app_hom, Iso.symm_hom]
      simp_rw [‚Üê Functor.whiskerLeft_app, ‚Üê NatTrans.comp_app]
      congr 1
      apply monoidalNatTrans_ext_app_singleton
      intro c
      simp)

/-- Any symmetric monoidal category can be interpreted as a pseudofunctor `Kleisli ‚•§·µñ Cat`, sending
`J` to the category of families `J ‚Üí C`, and using monoidal substitutions on arrow:
a morphism `œÜ : K ‚Üí SList J` is sent to the natural transformation sending
`X : J ‚Üí C` to the restriction along `œÜ` of the symmetric monoidal functor `SList J ‚•§ C` induced
by `X`.

For instance, for `K := Fin 1`, `J := Fin 2`, `œÜ : * ‚Ü¶ 0 ::~ [1]~` and `X := ![c‚ÇÅ, c‚ÇÇ]`,
((pseudoOfSymmMonCat (C : Type u')).map œÜ).obj X identifies to `c‚ÇÅ ‚äó c‚ÇÇ`. This precise
identification will be verified later (TODO). -/
@[simps!]
def pseudoOfSymmMonCat (C : Type u') [Category.{v'} C] [MonoidalCategory C] [SymmetricCategory C] :
    Kleisli.{0} ‚•§·µñ Cat.{v', u'} where
  obj X := .of <| X.of ‚Üí C
  map f := (extendedPullback C f).toCatHom
  map‚ÇÇ Œ∑ := NatTrans.toCatHom‚ÇÇ <| extendedPullback‚ÇÇ _ Œ∑
  mapId K := Cat.Hom.isoMk <|
    NatIso.ofComponents (fun X ‚Ü¶ Pi.isoMk fun i ‚Ü¶ monoidalLiftConsNilIso ..)
  mapComp {K K' K''} f g := Cat.Hom.isoMk <| extendedPullbackComp ..
  map‚ÇÇ_whisker_right {a b c} {f g} Œ∑ h := by
    ext (X : a.of ‚Üí C)
    dsimp
    ext i
    dsimp
    simp_rw [‚Üê NatTrans.comp_app, ‚Üê Functor.whiskerRight_app]
    congr 1
    apply monoidalNatTrans_ext_app_singleton
    intro c
    simp
  map‚ÇÇ_associator {a b c d} f g h := by
    ext (X : a.of ‚Üí C)
    dsimp
    ext i
    simp only [Pi.precompFunctor'_obj, associator_hom_hom, NatTrans.pi'_app, evaluation_map_app,
      Category.id_comp, Pi.comp_apply, Pi.isoMk_hom, Iso.app_hom, Iso.symm_hom,
      Pi.precompFunctor'_map, Pi.isoMk_inv, Iso.app_inv, Iso.symm_inv]
    /- again, there is an abuse of functor associativity, when trying to go back to the level of
    natural transforms, this time the fastest way is to manually add the correction term -/
    rw [‚Üê Category.comp_id
      ((RelativePseudomonad.Œº X f.of).hom.app ((monoidalLift g.of).obj (h.of i))),
      show (ùüô ((monoidalLift f.of ‚ãô monoidalLift X).obj ((monoidalLift g.of).obj (h.of i)))) =
      (Functor.associator _ (monoidalLift f.of) (monoidalLift X)).inv.app (h.of i) by simp]
    simp_rw [‚Üê Functor.whiskerRight_app, ‚Üê Functor.whiskerLeft_app, ‚Üê NatTrans.comp_app]
    congr 1
    apply monoidalNatTrans_ext_app_singleton
    intro c
    simp
  map‚ÇÇ_left_unitor {a b} f := by
    ext (X : a.of ‚Üí C)
    dsimp
    ext i
    simp only [Pi.precompFunctor'_obj, leftUnitor_hom_hom, NatTrans.pi'_app, evaluation_map_app,
      Iso.hom_inv_id, Pi.comp_apply, Pi.isoMk_hom, Iso.app_hom, Iso.symm_hom,
      Pi.precompFunctor'_map, Pi.id_apply]
    rw [show ùüô ((monoidalLift X).obj (f.of i)) =
      (monoidalLift X).leftUnitor.inv.app (f.of i) by rfl]
    simp_rw [‚Üê Functor.whiskerRight_app, ‚Üê NatTrans.comp_app]
    congr 1
    apply monoidalNatTrans_ext_app_singleton
    intro c
    simp

end Kleisli

end CategoryTheory.SList
